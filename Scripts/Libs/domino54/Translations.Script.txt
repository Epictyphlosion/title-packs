// -------------------------------------- //
//  LIBRARY TRANSLATIONS by domino54      //
//  script version: 2015-10-31            //
// -------------------------------------- //

/**
 *	This library allows to display translated messages,
 *	if they're featured in dedicated SentenceBank library.
 *	Instead TextLib compose function, use arrays: Text[].
 *	Chat messages MUST be prefixed.
 */

#Const Version		"2015-10-31"
#Const ScriptName	"Translations.Script.txt"

#Include "TextLib" as TL
#Include "Libs/Nadeo/Message.Script.txt" as Message
#Include "Libs/domino54/SentenceBank.Script.txt" as SB

// ---------------------------------- //
// Constants
// ---------------------------------- //
#Const C_DefaultChatPrefix "$888Â» "	///< Default chat message prefix

// ---------------------------------- //
// Global variables
// ---------------------------------- //
declare Text G_LibTranslations_ChatPrefix;	///< Custom chat message prefix

// ---------------------------------- //
// Functions
// ---------------------------------- //

// ---------------------------------- //
// Private
// ---------------------------------- //

// ---------------------------------- //
/** Return translation depending on selected language
 *
 *	@param	_Language		The language sentence will be translated to
 *	@param	_Sentence		The sentence to translate
 *
 *	@return		Translated sentence
 */
Text Private_GetTranslation(Text _Language, Text _Sentence) {
	if (_Sentence == "" || _Language == "" || _Language == "en" || _Language == "en_GB") return _Sentence;
	
	// Get sentence index
	declare SentenceIndex = SB::GetIndex(_Sentence);
	if (SentenceIndex == SB::UnknownIndex()) return _Sentence;
	
	// Translate sentence to target language
	declare Text TranslatedSentence;
	switch (_Language) {
		case "cz" : TranslatedSentence = SB::GetCzech(SentenceIndex);
		case "de" : TranslatedSentence = SB::GetGerman(SentenceIndex);
		case "es" : TranslatedSentence = SB::GetSpanish(SentenceIndex);
		case "fr" : TranslatedSentence = SB::GetFrench(SentenceIndex);
		case "hr" : TranslatedSentence = SB::GetCroatian(SentenceIndex);
		case "nl" : TranslatedSentence = SB::GetDutch(SentenceIndex);
		case "pl" : TranslatedSentence = SB::GetPolish(SentenceIndex);
		case "pt" : TranslatedSentence = SB::GetPortuguese(SentenceIndex);
		case "pt_BR" : TranslatedSentence = SB::GetPortuguese(SentenceIndex);
		case "ru" : TranslatedSentence = SB::GetRussian(SentenceIndex);
		case "sk" : TranslatedSentence = SB::GetCzech(SentenceIndex);
		default : return _Sentence;
	}
	
	if (TranslatedSentence == SB::NotFoundString()) return _Sentence;
	return TranslatedSentence;
}

// ---------------------------------- //
/** Return translation using Compose function
 *
 *	@param	_Language		The language sentences will be translated to
 *	@param	_Prefix			Text to attach before basic argument
 *	@param	_Arguments		The arguments for the compose function
 *	@param	_Suffix			Text to attach after basic argument
 *
 *	@return		Translated sentence
 */
Text Private_GetComposedTranslation(Text _Language, Text _Prefix, Text[] _Arguments, Text _Suffix) {
	if (_Arguments.count <= 0) return _Prefix^_Suffix;
	if (_Arguments.count == 1) return _Prefix^Private_GetTranslation(_Language, _Arguments[0])^_Suffix;
	
	// Translate all arguments
	declare Text[] Args;
	foreach (I => Argument in _Arguments) if (I <= 5) Args.add(Private_GetTranslation(_Language, Argument));
	
	// Attach prefix and suffix
	Args[0] = _Prefix^Args[0]^_Suffix;
	
	// Compose arguments
	switch (Args.count - 1) {
		case 5 : return TL::Compose(Args[0], Args[1], Args[2], Args[3], Args[4], Args[5]);
		case 4 : return TL::Compose(Args[0], Args[1], Args[2], Args[3], Args[4]);
		case 3 : return TL::Compose(Args[0], Args[1], Args[2], Args[3]);
		case 2 : return TL::Compose(Args[0], Args[1], Args[2]);
		case 1 : return TL::Compose(Args[0], Args[1]);
	}
	return TL::Compose(Args[0]);
}

// ---------------------------------- //
// Public
// ---------------------------------- //

// ---------------------------------- //
/** Return the version number of the script
 *
 *	@return		The version number of the script
 */
Text GetScriptVersion() {
	return Version;
}

// ---------------------------------- //
/** Return the name of the script
 *
 *	@return		The name of the script
 */
Text GetScriptName() {
	return ScriptName;
}

// ---------------------------------- //
/** Set custom chat prefix
 *
 *	@param	_Prefix		Text to attach before translations
 */
Void SetChatPrefix(Text _Prefix) { G_LibTranslations_ChatPrefix = _Prefix; }

// ---------------------------------- //
/** Send chat message to specified player
 *
 *	@param	_Player			Player to receive a message
 *	@param	_Message		Message to send
 */
Void SendChat(CPlayer _Player, Text _Message) {
	if (_Message == "" || _Player == Null || _Player.User == Null || _Player.User.IsFakeUser) return;
	
	// Load default chat prefix
	if (G_LibTranslations_ChatPrefix == "") G_LibTranslations_ChatPrefix = C_DefaultChatPrefix;
	
	declare UI <=> UIManager.GetUI(_Player);
	if (UI != Null) UI.SendChat(Private_GetTranslation(_Player.User.Language, _Message));
}

// ---------------------------------- //
/** Send chat message to all players
 *
 *	@param	_Message		Message be send
 */
Void SendChat(Text _Message) {
	foreach (Player in AllPlayers) SendChat(Player, _Message);
}

// ---------------------------------- //
/** Send composed chat message to specified player
 *
 *	@param	_Player			Player to receive a message
 *	@param	_Arguments		Arguments of the message to send
 */
Void SendChat(CPlayer _Player, Text[] _Arguments) {
	if (_Arguments.count <= 0 || _Player == Null || _Player.User == Null || _Player.User.IsFakeUser) return;
	
	// Load default chat prefix
	if (G_LibTranslations_ChatPrefix == "") G_LibTranslations_ChatPrefix = C_DefaultChatPrefix;
	
	declare UI <=> UIManager.GetUI(_Player);
	if (UI != Null) UI.SendChat(Private_GetComposedTranslation(_Player.User.Language, G_LibTranslations_ChatPrefix, _Arguments, ""));
}

// ---------------------------------- //
/** Send composed chat message to all players
 *
 *	@param	_Arguments		Arguments of the message to send
 */
Void SendChat(Text[] _Arguments) {
	foreach (Player in AllPlayers) SendChat(Player, _Arguments);
}

// ---------------------------------- //
/** Send notice to specified player (SM)
 *
 *	@param	_Player				Player to receive a notice
 *	@param	_Notice				Notice to send
 *	@param	_Level				Level of the notice
 *	@param	_Avatar				User to use avatar as icon
 *	@param	_AvatarVariant		Variant of the user's avatar
 *	@param	_Sound				Notice sound
 *	@param	_SoundVariant		Variant of the notice sound
 */
Void SendNotice(CPlayer _Player, Text _Notice, CUIConfig::ENoticeLevel _Level, CUser _Avatar, CUIConfig::EAvatarVariant _AvatarVariant, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	if (_Notice == "" || _Player == Null || _Player.User == Null || _Player.User.IsFakeUser) return;
	
	declare UI <=> UIManager.GetUI(_Player);
	if (UI != Null) UI.SendNotice(Private_GetTranslation(_Player.User.Language, _Notice), _Level, _Avatar, _AvatarVariant, _Sound, _SoundVariant);
}

// ---------------------------------- //
/** Send notice to all players (SM)
 *
 *	@param	_Notice				Notice to send
 *	@param	_Level				Level of the notice
 *	@param	_Avatar				User to use avatar as icon
 *	@param	_AvatarVariant		Variant of the user's avatar
 *	@param	_Sound				Notice sound
 *	@param	_SoundVariant		Variant of the notice sound
 */
Void SendNotice(Text _Notice, CUIConfig::ENoticeLevel _Level, CUser _Avatar, CUIConfig::EAvatarVariant _AvatarVariant, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	foreach (Player in AllPlayers) SendNotice(Player, _Notice, _Level, _Avatar, _AvatarVariant, _Sound, _SoundVariant);
}

// ---------------------------------- //
/** Send composed notice to specified player (SM)
 *
 *	@param	_Player				Player to receive a notice
 *	@param	_Arguments			Arguments of the notice to send
 *	@param	_Level				Level of the notice
 *	@param	_Avatar				User to use avatar as icon
 *	@param	_AvatarVariant		Variant of the user's avatar
 *	@param	_Sound				Notice sound
 *	@param	_SoundVariant		Variant of the notice sound
 */
Void SendNotice(CPlayer _Player, Text[] _Arguments, CUIConfig::ENoticeLevel _Level, CUser _Avatar, CUIConfig::EAvatarVariant _AvatarVariant, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	if (_Arguments.count <= 0 || _Player == Null || _Player.User == Null || _Player.User.IsFakeUser) return;
	
	declare UI <=> UIManager.GetUI(_Player);
	if (UI != Null) UI.SendNotice(Private_GetComposedTranslation(_Player.User.Language, "", _Arguments, ""), _Level, _Avatar, _AvatarVariant, _Sound, _SoundVariant);
}

// ---------------------------------- //
/** Send composed notice to all players (SM)
 *
 *	@param	_Arguments			Arguments of the notice to send
 *	@param	_Level				Level of the notice
 *	@param	_Avatar				User to use avatar as icon
 *	@param	_AvatarVariant		Variant of the user's avatar
 *	@param	_Sound				Notice sound
 *	@param	_SoundVariant		Variant of the notice sound
 */
Void SendNotice(Text[] _Arguments, CUIConfig::ENoticeLevel _Level, CUser _Avatar, CUIConfig::EAvatarVariant _AvatarVariant, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	foreach (Player in AllPlayers) SendNotice(Player, _Arguments, _Level, _Avatar, _AvatarVariant, _Sound, _SoundVariant);
}

// ---------------------------------- //
/** Insert library functions inside a manialink
 *
 *	@param	_Sentences		List of sentences to include in manialink
 *
 *	@return		Text containing generated functions
 */
Text InsertToManialink(Text[] _Sentences) {
	if (_Sentences.count <= 0) return "";
	
	// Create languages sentence banks
	declare Text[Text] SentenceBanks;
	declare Languages = ["en", "cz", "de", "es", "fr", "hr", "nl", "pl", "pt", "ru", "sk"];
	foreach (Language in Languages) SentenceBanks[Language] = "-2 => \"\"";
	
	// Fill banks with available translations
	foreach (Sentence in _Sentences) {
		declare Index = SB::GetIndex(Sentence);
		if (Index != SB::UnknownIndex()) {
			SentenceBanks["en"] ^= ", "^Index^" => \""^Sentence^"\"";
			SentenceBanks["cz"] ^= ", "^Index^" => \""^SB::GetCzech(Index)^"\"";
			SentenceBanks["de"] ^= ", "^Index^" => \""^SB::GetGerman(Index)^"\"";
			SentenceBanks["es"] ^= ", "^Index^" => \""^SB::GetSpanish(Index)^"\"";
			SentenceBanks["fr"] ^= ", "^Index^" => \""^SB::GetFrench(Index)^"\"";
			SentenceBanks["hr"] ^= ", "^Index^" => \""^SB::GetCroatian(Index)^"\"";
			SentenceBanks["nl"] ^= ", "^Index^" => \""^SB::GetDutch(Index)^"\"";
			SentenceBanks["pl"] ^= ", "^Index^" => \""^SB::GetPolish(Index)^"\"";
			SentenceBanks["pt"] ^= ", "^Index^" => \""^SB::GetPortuguese(Index)^"\"";
			SentenceBanks["ru"] ^= ", "^Index^" => \""^SB::GetRussian(Index)^"\"";
			SentenceBanks["sk"] ^= ", "^Index^" => \""^SB::GetSlovak(Index)^"\"";
		}
	}
	
	return """
// ---------------------------------- //
// Translations Start
// ---------------------------------- //

Integer LibTranslations_GetIndex(Text _Sentence) {
	declare C_LibSB_Index = [{{{SentenceBanks["en"]}}}];
	if (C_LibSB_Index.exists(_Sentence)) return C_LibSB_Index.keyof(_Sentence);
	return {{{SB::UnknownIndex()}}};
}

Text LibTranslations_GetCzech(Integer _Index) {
	declare C_LibSB_Czech = [{{{SentenceBanks["cz"]}}}];
	if (C_LibSB_Czech.existskey(_Index)) return C_LibSB_Czech[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetGerman(Integer _Index) {
	declare C_LibSB_German = [{{{SentenceBanks["de"]}}}];
	if (C_LibSB_German.existskey(_Index)) return C_LibSB_German[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetSpanish(Integer _Index) {
	declare C_LibSB_Spanish = [{{{SentenceBanks["es"]}}}];
	if (C_LibSB_Spanish.existskey(_Index)) return C_LibSB_Spanish[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetFrench(Integer _Index) {
	declare C_LibSB_French = [{{{SentenceBanks["fr"]}}}];
	if (C_LibSB_French.existskey(_Index)) return C_LibSB_French[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetCroatian(Integer _Index) {
	declare C_LibSB_Croatian = [{{{SentenceBanks["hr"]}}}];
	if (C_LibSB_Croatian.existskey(_Index)) return C_LibSB_Croatian[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetDutch(Integer _Index) {
	declare C_LibSB_Dutch = [{{{SentenceBanks["nl"]}}}];
	if (C_LibSB_Dutch.existskey(_Index)) return C_LibSB_Dutch[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetPolish(Integer _Index) {
	declare C_LibSB_Polish = [{{{SentenceBanks["pl"]}}}];
	if (C_LibSB_Polish.existskey(_Index)) return C_LibSB_Polish[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetPortuguese(Integer _Index) {
	declare C_LibSB_Portuguese = [{{{SentenceBanks["pt"]}}}];
	if (C_LibSB_Portuguese.existskey(_Index)) return C_LibSB_Portuguese[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetRussian(Integer _Index) {
	declare C_LibSB_Russian = [{{{SentenceBanks["ru"]}}}];
	if (C_LibSB_Russian.existskey(_Index)) return C_LibSB_Russian[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text LibTranslations_GetSlovak(Integer _Index) {
	declare C_LibSB_Slovak = [{{{SentenceBanks["sk"]}}}];
	if (C_LibSB_Slovak.existskey(_Index)) return C_LibSB_Slovak[_Index];
	return "{{{SB::NotFoundString()}}}";
}

Text Translations_Get(Text _Sentence) {
	if (_Sentence == "" || LocalUser.Language == "en" || LocalUser.Language == "en_GB") return _Sentence;
	
	declare SentenceIndex = LibTranslations_GetIndex(_Sentence);
	if (SentenceIndex == {{{SB::UnknownIndex()}}}) return _Sentence;
	
	declare Text TranslatedSentence;
	switch (LocalUser.Language) {
		case "cz" : TranslatedSentence = LibTranslations_GetCzech(SentenceIndex);
		case "de" : TranslatedSentence = LibTranslations_GetGerman(SentenceIndex);
		case "es" : TranslatedSentence = LibTranslations_GetSpanish(SentenceIndex);
		case "fr" : TranslatedSentence = LibTranslations_GetFrench(SentenceIndex);
		case "hr" : TranslatedSentence = LibTranslations_GetCroatian(SentenceIndex);
		case "nl" : TranslatedSentence = LibTranslations_GetDutch(SentenceIndex);
		case "pl" : TranslatedSentence = LibTranslations_GetPolish(SentenceIndex);
		case "pt" : TranslatedSentence = LibTranslations_GetPortuguese(SentenceIndex);
		case "pt_BR" : TranslatedSentence = LibTranslations_GetPortuguese(SentenceIndex);
		case "ru" : TranslatedSentence = LibTranslations_GetRussian(SentenceIndex);
		case "sk" : TranslatedSentence = LibTranslations_GetCzech(SentenceIndex);
		default : return _Sentence;
	}
	
	if (TranslatedSentence == "{{{SB::NotFoundString()}}}") return _Sentence;
	return TranslatedSentence;
}

Text Translations_GetComposed(Text[] _Arguments) {
	if (_Arguments.count == 1) return Translations_Get(_Arguments[0]);
	
	declare Text[] Args;
	foreach (I => Argument in _Arguments) if (I <= 5) Args.add(Translations_Get(Argument));
	
	switch (Args.count - 1) {
		case 5 : return TL::Compose(Args[0], Args[1], Args[2], Args[3], Args[4], Args[5]);
		case 4 : return TL::Compose(Args[0], Args[1], Args[2], Args[3], Args[4]);
		case 3 : return TL::Compose(Args[0], Args[1], Args[2], Args[3]);
		case 2 : return TL::Compose(Args[0], Args[1], Args[2]);
		case 1 : return TL::Compose(Args[0], Args[1]);
	}
	return TL::Compose(Args[0]);
}

// ---------------------------------- //
// Translations Stop
// ---------------------------------- //
	""";
}

// ---------------------------------- //
// Message library extension
// ---------------------------------- //

Void SendBigMessage(CPlayer _Player, Text _Message, Integer _Duration, Integer _Priority, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	if (_Player == Null || _Player.User == Null) return;
	Message::SendBigMessage(_Player, Private_GetTranslation(_Player.User.Language, _Message), _Duration, _Priority, _Sound, _SoundVariant);
}

Void SendBigMessage(Text _Message, Integer _Duration, Integer _Priority, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	foreach (Player in AllPlayers) SendBigMessage(Player, _Message, _Duration, _Priority, _Sound, _SoundVariant);
}

Void SendBigMessage(CPlayer _Player, Text[] _Arguments, Integer _Duration, Integer _Priority, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	if (_Player == Null || _Player.User == Null) return;
	Message::SendBigMessage(_Player, Private_GetComposedTranslation(_Player.User.Language, "", _Arguments, ""), _Duration, _Priority, _Sound, _SoundVariant);
}

Void SendBigMessage(Text[] _Arguments, Integer _Duration, Integer _Priority, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	foreach (Player in AllPlayers) SendBigMessage(Player, _Arguments, _Duration, _Priority, _Sound, _SoundVariant);
}

Void SendStatusMessage(CPlayer _Player, Text _Message, Integer _Duration, Integer _Priority, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	if (_Player == Null || _Player.User == Null) return;
	Message::SendStatusMessage(_Player, Private_GetTranslation(_Player.User.Language, _Message), _Duration, _Priority, _Sound, _SoundVariant);
}

Void SendStatusMessage(Text _Message, Integer _Duration, Integer _Priority, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	foreach (Player in AllPlayers) SendStatusMessage(Player, _Message, _Duration, _Priority, _Sound, _SoundVariant);
}

Void SendStatusMessage(CPlayer _Player, Text[] _Arguments, Integer _Duration, Integer _Priority, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	if (_Player == Null || _Player.User == Null) return;
	Message::SendStatusMessage(_Player, Private_GetComposedTranslation(_Player.User.Language, "", _Arguments, ""), _Duration, _Priority, _Sound, _SoundVariant);
}

Void SendStatusMessage(Text[] _Arguments, Integer _Duration, Integer _Priority, CUIConfig::EUISound _Sound, Integer _SoundVariant) {
	foreach (Player in AllPlayers) SendStatusMessage(Player, _Arguments, _Duration, _Priority, _Sound, _SoundVariant);
}

Void SendBigMessage(CPlayer _Player, Text _Message, Integer _Duration, Integer _Priority) {
	if (_Player == Null || _Player.User == Null) return;
	Message::SendBigMessage(_Player, Private_GetTranslation(_Player.User.Language, _Message), _Duration, _Priority, CUIConfig::EUISound::Silence, 0);
}

Void SendBigMessage(Text _Message, Integer _Duration, Integer _Priority) {
	foreach (Player in AllPlayers) SendBigMessage(Player, _Message, _Duration, _Priority, CUIConfig::EUISound::Silence, 0);
}

Void SendBigMessage(CPlayer _Player, Text[] _Arguments, Integer _Duration, Integer _Priority) {
	if (_Player == Null || _Player.User == Null) return;
	Message::SendBigMessage(_Player, Private_GetComposedTranslation(_Player.User.Language, "", _Arguments, ""), _Duration, _Priority, CUIConfig::EUISound::Silence, 0);
}

Void SendBigMessage(Text[] _Arguments, Integer _Duration, Integer _Priority) {
	foreach (Player in AllPlayers) SendBigMessage(Player, _Arguments, _Duration, _Priority, CUIConfig::EUISound::Silence, 0);
}

Void SendStatusMessage(CPlayer _Player, Text _Message, Integer _Duration, Integer _Priority) {
	if (_Player == Null || _Player.User == Null) return;
	Message::SendStatusMessage(_Player, Private_GetTranslation(_Player.User.Language, _Message), _Duration, _Priority, CUIConfig::EUISound::Silence, 0);
}

Void SendStatusMessage(Text _Message, Integer _Duration, Integer _Priority) {
	foreach (Player in AllPlayers) SendStatusMessage(Player, _Message, _Duration, _Priority, CUIConfig::EUISound::Silence, 0);
}

Void SendStatusMessage(CPlayer _Player, Text[] _Arguments, Integer _Duration, Integer _Priority) {
	if (_Player == Null || _Player.User == Null) return;
	Message::SendStatusMessage(_Player, Private_GetComposedTranslation(_Player.User.Language, "", _Arguments, ""), _Duration, _Priority, CUIConfig::EUISound::Silence, 0);
}

Void SendStatusMessage(Text[] _Arguments, Integer _Duration, Integer _Priority) {
	foreach (Player in AllPlayers) SendStatusMessage(Player, _Arguments, _Duration, _Priority, CUIConfig::EUISound::Silence, 0);
}
