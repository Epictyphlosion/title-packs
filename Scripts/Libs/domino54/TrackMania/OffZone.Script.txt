// -------------------------------------- //
//  OFFZONE LIB by domino54               //
//  script version: 2015-07-23            //
//  - in development -                    //
// -------------------------------------- //

/**
 *	This library allows mode to read custom OffZone locations
 *	stored in map metadata to use it as zones respawning
 *	players if they drive into them.
 *
 *	To define custom OffZone areas in your maps, use the
 *	OffZonePlacement editor plugin, available to download
 *	for free from domino54's GitHub repository.
 */

#Const Version		"2015-07-23"
#Const ScriptName	"OffZone.Script.txt"

#Include "MathLib" as ML
#Include "Libs/Nadeo/TrackMania/TM2.Script.txt" as TM2

// ---------------------------------- //
// Constants
// ---------------------------------- //

/// Size of the block in each environment
#Const C_LibOffZone_ColBlockDimensions [
	"Canyon" => <64., 16., 64.>,
	"Stadium" => <32., 8., 32.>,
	"Valley" => <32., 8., 32.>
]

/// Y index of the block in position 0 in each environment
#Const C_LibOffZone_ColGroundCoordY [
	"Canyon" => 11,
	"Stadium" => 9,
	"Valley" => 9
]

// ---------------------------------- //
// Global variables
// ---------------------------------- //
declare Boolean G_LibOffZone_IsLoaded;				///< True, if environment dimensions loaded correctly
declare Vec3[] G_LibOffZone_ColBlockDimensions;	///< Size of current environment blocks in meters
declare Integer G_LibOffZone_ColGroundCoordY;		///< Y index of block in position <0., 0., 0.>
declare Int3[] G_LibOffZone_OffZoneLocations;		///< Positions of the blocks containing OffZone triggers

/// Functions activity settings
declare Boolean G_LibOffZone_DisableOffZone; ///< Disable event when Player touch OffZone block
declare Boolean G_LibOffZone_DisableFalling; ///< Disable event when Player falls under construction area
declare Boolean G_LibOffZone_DisableBorders; ///< Disable event when Player drives beyond map edge

// ---------------------------------- //
// Functions
// ---------------------------------- //

// ---------------------------------- //
/** Get coordinates of the block Player is standing in
 *
 *	@param _Position	World position of the Player
 *
 *	@return		Coordinates of the block
 */
Int3 Private_GetPlayerBlockCoords(Vec3 _Position) {
	if (!G_LibOffZone_IsLoaded) return Int3;
	
	declare Int3 BlockCoords;
	for (I, 0, 2) BlockCoords[I] = ML::FloorInteger(_Position[I] / G_LibOffZone_ColBlockDimensions[I]);
	BlockCoords.Y += G_LibOffZone_ColGroundCoordY;
}

// ---------------------------------- //
// Public
// ---------------------------------- //

// ---------------------------------- //
/** Return the version number of the script
 *
 *	@return		The version number of the script
 */
Text GetScriptVersion() {
	return Version;
}

// ---------------------------------- //
/** Return the name of the script
 *
 *	@return		The name of the script
 */
Text GetScriptName() {
	return ScriptName;
}

// ---------------------------------- //
/// Unload map OffZone locations
Void EndMap() {
	G_LibOffZone_IsLoaded = False;
	G_LibOffZone_ColBlockDimensions = Vec3;
	G_LibOffZone_ColGroundCoordY = -1;
	G_LibOffZone_OffZoneLocations.clear();
}

// ---------------------------------- //
/// Load map OffZone locations
Void StartMap() {
	if (Map == Null) return;
	EndMap();
	
	// ---------------------------------- //
	// Get current collection block size
	if (C_LibOffZone_ColBlockDimensions.existskey(Map.CollectionName))
		G_LibOffZone_ColBlockDimensions = C_LibOffZone_ColBlockDimensions[Map.CollectionName];
	else return;
	
	// ---------------------------------- //
	// Cet current collection ground Y
	if (G_LibOffZone_ColGroundCoordY.existskey(Map.CollectionName))
		G_LibOffZone_ColGroundCoordY = G_LibOffZone_ColGroundCoordY[Map.CollectionName];
	else return;
	
	// Environment loaded correctly
	G_LibOffZone_IsLoaded = True;
	
	// ---------------------------------- //
	// Restore metadata from the map file
	declare metadata Int3[] TMOffZoneLocations for Map;
	if (TMOffZoneLocations.count == 0) return;
	G_LibOffZone_OffZoneLocations = TMOffZoneLocations;
}

// ---------------------------------- //
/** Check if Player is in OffZone
 *
 *	@param _Player	Player to check status
 *
 *	@return		True if Player is in OffZone, False otherwise
 */
Boolean IsInOffZone(CTmPlayer _Player) {
	if (G_LibOffZone_DisableOffZone || _Player == Null) return False;
	if (!G_LibOffZone_IsLoaded || G_LibOffZone_OffZoneLocations.count == 0) return False;
	if (_Player.IsSpawned == False || _Player.Position == <0., 0., 0.> || _Now < Player.RaceStartTime) return False;
	
	return G_LibOffZone_OffZoneLocations.exists(Private_GetPlayerBlockCoords(_Player.Position));
}

// ---------------------------------- //
/** Check if Player is below map construction zone
 *
 *	@param _Player	Player to check status
 *
 *	@return		True if Player is below construction area, False otherwise
 */
Boolean IsBelowMapBottom(CTmPlayer _Player) {
	if (!G_LibOffZone_IsLoaded || G_LibOffZone_DisableFalling || _Player == Null) return False;
	if (_Player.IsSpawned == False || _Player.Position == <0., 0., 0.> || _Now < Player.RaceStartTime) return False;
	
	return (_Player.Position.Y <= G_LibOffZone_ColBlockDimensions.Y * -G_LibOffZone_ColGroundCoordY);
}

// ---------------------------------- //
/** Check if Player is beyond map border
 *
 *	@param _Player	Player to check status
 *
 *	@return		True if Player is beyond map border, False otherwise
 */
Boolean IsBeyondMapEdge(CTmPlayer _Player) {
	if (!G_LibOffZone_IsLoaded || G_LibOffZone_DisableBorders || _Player == Null) return False;
	if (_Player.IsSpawned == False || _Player.Position == <0., 0., 0.> || _Now < Player.RaceStartTime) return False;
	
	return (
		_Player.Position.X < 0 || _Player.Position.Z < 0 ||
		_Player.Position.X > G_LibOffZone_ColBlockDimensions.X * Map.Size.X
		_Player.Position.Z > G_LibOffZone_ColBlockDimensions.Z * Map.Size.Z
	);
}

// ---------------------------------- //
/// Handle necessary events automatically
Void Loop() {
	if (!G_LibOffZone_IsLoaded) return;
	
	// ---------------------------------- //
	// Events management
	foreach (Player in Players) {
		// ---------------------------------- //
		// Unspawn players in OffZone
		if (!G_LibOffZone_DisableOffZone && IsInOffZone(Player)) TM2::WaitRace(Player);
		
		// ---------------------------------- //
		// Unspawn players falling under map
		if (!G_LibOffZone_DisableFalling && IsBelowMapBottom(Player)) TM2::WaitRace(Player);

		// ---------------------------------- //
		// Unspawn players beyond map edges
		if (!G_LibOffZone_DisableBorders && IsBeyondMapEdge(Player)) TM2::WaitRace(Player);
	}
}

// ---------------------------------- //
/** Change OffZone activity setting
 *
 *	@param _UseOffZone	True if OffZone has to be active
 */
Void SetUseOffZone(Boolean _UseOffZone) {
	G_LibOffZone_DisableOffZone = !_UseOffZone;
}

// ---------------------------------- //
/** Change falling protection setting
 *
 *	@param _UseFallProtect	True if Players below map have to be unspawned
 */
Void SetUseFallProtection(Boolean _UseFallProtect) {
	G_LibOffZone_DisableFalling = !_UseFallProtect;
}

// ---------------------------------- //
/** Change map edge crossing setting
 *
 *	@param _UseMapEdges	True if Players beyond map edges have to be unspawned
 */
Void SetUseFallProtection(Boolean _UseMapEdges) {
	G_LibOffZone_DisableBorders = !_UseMapEdges;
}
