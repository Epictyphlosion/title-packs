// -------------------------------------- //
//  LIBRARY GALAXYTITLES by domino54      //
//  script version: 2016-09-25            //
// -------------------------------------- //

/**
 *	Works only in the title pack "GalaxyTitles@domino54".
 *	This library contains essential features for all
 *	game modes running in the GalaxyTitles title pack:
 *	-	Spawning and managing Fuel and Armor
 *	-	Spawning Combo weapon items and managing inventory
 *	-	Spawning and managing NPC Toads
 *	-	Managing players' reload boost
 */

#Const Version		"2016-09-25"
#Const ScriptName	"GalaxyTitles.Script.txt"

#Include "MathLib" as ML
#Include "TextLib" as TL
#Include "Libs/Nadeo/ShootMania/Score.Script.txt" as Score
#Include "Libs/domino54/Statistics.Script.txt" as Statistics
#Include "Libs/domino54/TopScores.Script.txt" as TopScores
#Include "Libs/domino54/ShootMania/Interfancy.Script.txt" as Interfancy
#Include "Libs/domino54/ShootMania/Inventory.Script.txt" as Inventory
#Include "Libs/domino54/ShootMania/MiniMap.Script.txt" as MiniMap

// ---------------------------------- //
// Constants
// ---------------------------------- //
#Const C_ImgBase "file://Media/Manialinks/ShootMania/GalaxyTitles/"
#Const C_LibGalaxyTitles_AcceptedObjects ["Armor", "Fuel", "Laser", "Nucleus", "Arrow", "Rocket"]
#Const C_LibGalaxyTitles_WeaponItemsTags ["Laser", "Nucleus", "Arrow", "Rocket"]

/// Objects spawning
#Const C_LibGalaxyTitles_ObjectsRespawnTime		15000			///< Time before object is respawned
#Const C_LibGalaxyTitles_ObjectsDropLifetime	5000			///< Time for how long dropped weapons stay under player
#Const C_LibGalaxyTitles_ObjectsDropSpreadSize	<.5, .25, .5>	///< Size of the possible objects spread radius

/// Objects functions
#Const C_LibGalaxyTitles_ArmorPickupRadius		10.		///< Armor item healing radius
#Const C_LibGalaxyTitles_ArmorHealAmount		100		///< Armor item heal amount
#Const C_LibGalaxyTitles_FuelPickupRadius		10.		///< Fuel item bonus radius
#Const C_LibGalaxyTitles_FuelReloadMultipler	3.		///< Fuel reload bonus multipler
#Const C_LibGalaxyTitles_FuelReloadDuration		2000	///< Fuel reload bonus duration
#Const C_LibGalaxyTitles_DefaultPointsForPick	1		///< Default amount of points for affecting other player
#Const C_LibGalaxyTitles_NoteDisplayTime		1750	///< Duration of the notice display time

/// Toads
#Const C_LibGalaxyTitles_ToadLandmarksRatio		.5		///< Minimal amount of landmark spawning Toads
#Const C_LibGalaxyTitles_ToadRespawnTime		60		///< Time before Toad is respawned
#Const C_LibGalaxyTitles_ToadChillingTime		5000	///< Toad wandering delay
#Const C_LibGalaxyTitles_ToadSaunterRadius		6.		///< Toad wandering area radius
#Const C_LibGalaxyTitles_ToadShootPeriod		1000	///< Toad shoot period
#Const C_LibGalaxyTitles_ToadSpeedPower			.25		///< Toad speed factor
#Const C_LibGalaxyTitles_ToadAmmoGain			.5		///< Toad ammo reload speed
#Const C_LibGalaxyTitles_ToadArmorMax			200		///< Amount of Toads armor points
#Const C_LibGalaxyTitles_ToadAggroRadius		8.		///< Distance within shoot provokes Toad
#Const C_LibGalaxyTitles_ToadPeaceDistance		16.		///< Distance within Toad will follow Player

#Const C_LibGalaxyTitles_MinimapColors [
	"Laser" => <1., 0., 1.>, "Nucleus" => <1., 0., .5>, "Arrow" => <1., 0., 0.>, "Rocket" => <1., 1., 0.>
]

// ---------------------------------- //
// Global variables
// ---------------------------------- //
declare Ident[Text] G_LibGalaxyTitles_Objects;
declare Boolean G_LibGalaxyTitles_LibraryLoaded;
declare Integer G_LibGalaxyTitles_PointsForPick;
declare Ident[] G_LibGalaxyTitles_ToadsSpawns;
declare Integer G_LibGalaxyTitles_ToadsArmorMax;
declare Boolean G_LibGalaxyTitles_WeaponItemsAvailable;
declare Text[] G_LibGalaxyTitles_AvailableWeapons;
declare Boolean G_LibGalaxyTitles_ObjectsAffectEveryone;
declare Integer G_LibGalaxyTitles_ObjectsDropLifetime;
declare Vec3[Integer] G_LibGalaxyTitles_PrevTeamsColors;

/// Elements created within library
declare Ident[] G_LibGalaxyTitles_CreatedObjects;
declare Ident[] G_LibGalaxyTitles_SpawnedToads;
declare Text[] G_LibGalaxyTitles_MinimapMarkers;
declare Ident[Integer] G_LibGalaxyTitles_DroppedObjects;

// ---------------------------------- //
// Functions
// ---------------------------------- //

// ---------------------------------- //
/** Get the third team color
 *
 *	@param	_Color1		Color of the first clan
 *	@param	_Color2		Color of the second clan
 *
 *	@return		Color of the third clan
 */
Vec3 Private_GetThirdClanColor(Vec3 _Color1, Vec3 _Color2) {
	declare Vec3 Sum;
	declare Real Max;
	for (I, 0, 2) {
		Sum[I] = _Color1[I] + _Color2[I];
		if (Sum[I] > Max) Max = Sum[I];
	}
	if (Max > 0) for (I, 0, 2) Sum[I] = 1. - (Sum[I] / Max);
	Max = 0.;
	for (I, 0, 2) if (Sum[I] > Max) Max = Sum[I];
	if (Max > 0) for (I, 0, 2) Sum[I] *= 1 / Max;
	return Sum;
}

// ---------------------------------- //
// Public
// ---------------------------------- //

// ---------------------------------- //
/** Return the version number of the script
 *
 *	@return		The version number of the script
 */
Text GetScriptVersion() { return Version; }

// ---------------------------------- //
/** Return the name of the script
 *
 *	@return		The name of the script
 */
Text GetScriptName() { return ScriptName; }

// ---------------------------------- //
/** Remove reload boost from specific Player
 *
 *	@param	_Player		The Player to remove boost
 */
Void ClearReloadBoost(CSmPlayer _Player) {
	declare LibGalaxyTitles_BoostEndTime for _Player = -1;
	LibGalaxyTitles_BoostEndTime = -1;
}

// ---------------------------------- //
// Remove reload boost from all players
Void ClearReloadBoost() {
	foreach (Player in AllPlayers) ClearReloadBoost(Player);
}

// ---------------------------------- //
/// Destroy all objects 
Void DestroyLibObjects() {
	foreach (ObjectId in G_LibGalaxyTitles_CreatedObjects) {
		declare CSmObject Object;
		if (Objects.existskey(ObjectId)) Object = Objects[ObjectId];
		if (Object == Null) continue;
		
		Object.SetUnspawned();
		ObjectDestroy(Object);
	}
	G_LibGalaxyTitles_CreatedObjects.clear();
	G_LibGalaxyTitles_DroppedObjects.clear();
	
	// Destroy markers
	foreach (MarkerTag in G_LibGalaxyTitles_MinimapMarkers) MiniMap::DestroyMarker(MarkerTag);
	G_LibGalaxyTitles_MinimapMarkers.clear();
}

// ---------------------------------- //
/// Destroy all Toads 
Void DestroyAllToads() {
	foreach (BotPlayerId in G_LibGalaxyTitles_SpawnedToads) {
		declare CSmPlayer BotPlayer;
		if (BotPlayers.existskey(BotPlayerId)) BotPlayer = BotPlayers[BotPlayerId];
		if (BotPlayer == Null) continue;
		
		UnspawnPlayer(BotPlayer);
		DestroyBotPlayer(BotPlayer);
	}
	G_LibGalaxyTitles_SpawnedToads.clear();
}

// ---------------------------------- //
/// Destroy everything
Void ResetAll() {
	ClearReloadBoost();
	DestroyLibObjects();
	DestroyAllToads();
}

// ---------------------------------- //
/// Unload library
Void Unload() {
	ResetAll();
	G_LibGalaxyTitles_ObjectsAffectEveryone = False;
	G_LibGalaxyTitles_PointsForPick = C_LibGalaxyTitles_DefaultPointsForPick;
	G_LibGalaxyTitles_ToadsArmorMax = C_LibGalaxyTitles_ToadArmorMax;
	G_LibGalaxyTitles_LibraryLoaded = False;
	
	// Unload libraries
	Inventory::Unload();
}

// ---------------------------------- //
/// Load library
Void Load() {
	Unload();
	G_LibGalaxyTitles_LibraryLoaded = True;
	G_LibGalaxyTitles_ObjectsDropLifetime = C_LibGalaxyTitles_ObjectsDropLifetime;
	
	// ---------------------------------- //
	// Load objects
	G_LibGalaxyTitles_Objects["Fuel"]		= ItemList_Add("SMCommon\\Collectibles\\Fuel.Item.gbx");
	G_LibGalaxyTitles_Objects["Armor"]		= ItemList_Add("SMCommon\\Pickups\\Armor.Item.gbx");
	G_LibGalaxyTitles_Objects["Laser"]		= ItemList_Add("SMCommon\\Pickups\\Laser.Item.gbx");
	G_LibGalaxyTitles_Objects["Nucleus"]	= ItemList_Add("SMCommon\\Pickups\\Nucleus.Item.gbx");
	G_LibGalaxyTitles_Objects["Arrow"]		= ItemList_Add("SMCommon\\Pickups\\Arrow.Item.gbx");
	G_LibGalaxyTitles_Objects["Rocket"]		= ItemList_Add("SMCommon\\Pickups\\Rocket.Item.gbx");
	G_LibGalaxyTitles_Objects["Toad"]		= ItemList_Add("Characters\\ToadSample.Item.gbx");
	
	// ---------------------------------- //
	// Load interface
	Interfancy::SetGaugesTextFont("Eurostile_Bold");
	Interfancy::SetBoostIconImage(C_ImgBase^"Icons/ReloadBoost.png");
	Interfancy::SetGaugesIconsImages([
		"Laser"		=> C_ImgBase^"Icons/Laser.png",
		"Rocket"	=> C_ImgBase^"Icons/Rocket.png",
		"Nucleus"	=> C_ImgBase^"Icons/Nucleus.png",
		"Arrow"		=> C_ImgBase^"Icons/Arrow.png",
		"Armor"		=> C_ImgBase^"Icons/Armor.png",
		"Stamina"	=> C_ImgBase^"Icons/Jump.png",
		"Sprint"	=> C_ImgBase^"Icons/Run.png"
	]);
	Interfancy::SetGaugesSoundPath([
		"AmmoStep"		=> C_ImgBase^"SoundGauge/UIGaugeEnergyBallSlot.wav",
		"ArmorStep"		=> C_ImgBase^"SoundGauge/UIGaugeHealthGradFull.wav",
		"StaminaFull"	=> C_ImgBase^"SoundGauge/UIGaugeStaminaFull.wav",
		"StaminaEmpty"	=> C_ImgBase^"SoundGauge/UIGaugeStaminaEmpty.wav"
	]);
	
	// ---------------------------------- //
	// Load and format Inventory
	Inventory::Load();
	Inventory::FormatInterfaceHotbar(
		<158., -74., 5.>, <8., 8.>, 1., 2.,
		CMlControl::AlignHorizontal::Right, CMlControl::AlignVertical::VCenter
	);
	
	Inventory::FormatLabelAmount("<label posn=\"4 -2.5\" valign=\"center2\" textfont=\"Eurostile_Bold\"/>");
	Inventory::FormatLabelHotkey("<label posn=\"-3.5 3\" valign=\"center2\" textfont=\"Eurostile_Bold\"/>");
	Inventory::FormatLabelCooldown("<label textsize=\"3\" textfont=\"Eurostile_Bold\"/>");
	Inventory::FormatQuadBackground("<quad sizen=\"8 8\" image=\""^C_ImgBase^"UiItemShadow.png\"/>");
	Inventory::FormatQuadHighlight("<quad sizen=\"10 10\" image=\""^C_ImgBase^"UiCurrentWeapon.png\"/>");
	
	Inventory::SetWeaponsIcons([
		CSmMode::EWeapon::Laser		=> C_ImgBase^"Icons/Laser.png",
		CSmMode::EWeapon::Nucleus	=> C_ImgBase^"Icons/Nucleus.png",
		CSmMode::EWeapon::Arrow		=> C_ImgBase^"Icons/Arrow.png",
		CSmMode::EWeapon::Rocket	=> C_ImgBase^"Icons/Rocket.png"
	]);
}

// ---------------------------------- //
/// Apply and remove reload boost to the Players
Void ReloadBoost() {
	if (Players.count <= 0 || !G_LibGalaxyTitles_LibraryLoaded) return;
	
	foreach (Player in Players) {
		declare LibGalaxyTitles_BoostEndTime for Player = -1;
		declare LibGalaxyTitles_PrevAmmoGain for Player = -1.;
		declare LibGalaxyTitles_BoostMultipler for Player = 1.;
		
		// ---------------------------------- //
		// Increase player reload speed
		if (Now < LibGalaxyTitles_BoostEndTime) {
			if (LibGalaxyTitles_PrevAmmoGain < 0.) {
				LibGalaxyTitles_PrevAmmoGain = Player.AmmoGain;
				Player.AmmoGain = ML::Clamp(Player.AmmoGain * LibGalaxyTitles_BoostMultipler, 0., 10.);
			}
			// ---------------------------------- //
			// Player base reload speed changed during boost
			else if (
				Player.AmmoGain < 10 &&
				LibGalaxyTitles_PrevAmmoGain != Player.AmmoGain / C_LibGalaxyTitles_FuelReloadMultipler
			) {
				declare ReloadDelta = Player.AmmoGain - LibGalaxyTitles_PrevAmmoGain / LibGalaxyTitles_BoostMultipler;
				LibGalaxyTitles_PrevAmmoGain += ReloadDelta;
				Player.AmmoGain = ML::Clamp(LibGalaxyTitles_PrevAmmoGain * LibGalaxyTitles_BoostMultipler, 0., 10.);
			}
		}
		// ---------------------------------- //
		// Reverse reload speed back to the old value
		else if (LibGalaxyTitles_PrevAmmoGain >= 0) {
			Player.AmmoGain = ML::Clamp(LibGalaxyTitles_PrevAmmoGain, 0., 10.);
			LibGalaxyTitles_PrevAmmoGain = -1.;
		}
	}
}

// ---------------------------------- //
/** Give the Player reload boost for certain amount of time
 *
 *	@param	_Player			The Player to apply boost
 *	@param	_Duration		Reload boost duration
 *	@param	_Multipler		Reload boost multipler
 */
Void AddReloadBoost(CSmPlayer _Player, Integer _Duration, Real _Multipler) {
	if (_Player == Null || _Duration <= 0 || _Multipler <= 1. || !G_LibGalaxyTitles_LibraryLoaded) return;
	
	declare LibGalaxyTitles_BoostEndTime for _Player = -1;
	declare LibGalaxyTitles_BoostMultipler for _Player = 1.;
	
	if (LibGalaxyTitles_BoostEndTime < Now) LibGalaxyTitles_BoostEndTime = Now + _Duration;
	else LibGalaxyTitles_BoostEndTime += _Duration;
	LibGalaxyTitles_BoostMultipler = _Multipler;
	Interfancy::SetPlayerReloadData(_Player, Now, LibGalaxyTitles_BoostEndTime - Now);
}

Void AddReloadBoost(CSmPlayer _Player, Integer _Duration) {
	AddReloadBoost(_Player, _Duration, C_LibGalaxyTitles_FuelReloadMultipler);
}

Void AddReloadBoost(CSmPlayer _Player, Real _Multipler) {
	AddReloadBoost(_Player, C_LibGalaxyTitles_FuelReloadDuration, _Multipler);
}

Void AddReloadBoost(CSmPlayer _Player) {
	AddReloadBoost(_Player, C_LibGalaxyTitles_FuelReloadDuration, C_LibGalaxyTitles_FuelReloadMultipler);
}

// ---------------------------------- //
/** Destroy all objects and reset landmarks settings
 *
 *	@param	_UseWeapons		Combo weapon items usage
 */
Void InitObjects(Boolean _UseWeapons) {
	DestroyLibObjects();
	G_LibGalaxyTitles_WeaponItemsAvailable = False;
	G_LibGalaxyTitles_AvailableWeapons.clear();
	
	if (!G_LibGalaxyTitles_LibraryLoaded) return;
	
	foreach (MapLandmark in MapLandmarks_ObjectAnchor) {
		declare Boolean LibGalaxyTitles_ObjectSpawned for MapLandmark;
		declare Integer LibGalaxyTitles_ObjectNextSpawn for MapLandmark;
		LibGalaxyTitles_ObjectSpawned = False;
		LibGalaxyTitles_ObjectNextSpawn = Now;
		
		// Tell the library to use weapons
		if (_UseWeapons && C_LibGalaxyTitles_WeaponItemsTags.exists(MapLandmark.Tag)) {
			G_LibGalaxyTitles_WeaponItemsAvailable = True;
			if (!G_LibGalaxyTitles_AvailableWeapons.exists(MapLandmark.Tag))
				G_LibGalaxyTitles_AvailableWeapons.add(MapLandmark.Tag);
		}
	}
}

// ---------------------------------- //
//// Destroy all objects and reset landmarks settings
Void InitObjects() { InitObjects(True); }

// ---------------------------------- //
/** Spawn objects on landmarks
 *
 *	@param	_SpawnObjects	If False, objects won't be spawned
 */
Void SpawnObjects(Boolean _SpawnObjects) {
	if (!_SpawnObjects || !G_LibGalaxyTitles_LibraryLoaded) return;
	
	// ---------------------------------- //
	// Drop weapons under eliminated players
	foreach (Player in Players) {
		// Discard drop when it's disabled by game mode
		if (G_LibGalaxyTitles_ObjectsDropLifetime <= 0) continue;
		
		declare Integer LastTimeInOffZone for Player;
		if (Player.IsInOffZone) LastTimeInOffZone = Now;
		
		// Don't drop items, when player is still alive (not in Portals) or if player was eliminated by OffZone
		if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::Spawned || Now < LastTimeInOffZone + 250) continue;
		
		// Get Ids of the object anchors
		declare Ident[] PlayerWeaponsToDrop for Player;
		if (PlayerWeaponsToDrop.count <= 0) continue;
		
		foreach (MapLandmarkId in PlayerWeaponsToDrop) {
			if (!MapLandmarks_ObjectAnchor.existskey(MapLandmarkId)) continue;
			declare MapLandmark <=> MapLandmarks_ObjectAnchor[MapLandmarkId];
			
			// ---------------------------------- //
			// Create object
			declare Boolean LibGalaxyTitles_ObjectSpawned for MapLandmark;
			declare CSmObject Object;
			
			switch (MapLandmark.Tag) {
				case "Laser"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Laser"]);
				case "Nucleus"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Nucleus"]);
				case "Arrow"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Arrow"]);
				case "Rocket"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Rocket"]);
			}
			
			if (Object == Null) continue;
			LibGalaxyTitles_ObjectSpawned = True;
			
			// ---------------------------------- //
			// Set object properties
			declare Ident LibGalaxyTitles_AnchorId for Object;
			declare Text LibGalaxyTitles_Tag for Object;
			LibGalaxyTitles_AnchorId = MapLandmarkId;
			LibGalaxyTitles_Tag = MapLandmark.Tag;
			
			// Reset weapon carrier
			declare Ident LibGalaxyTitles_ObjectPlayerId for MapLandmark;
			LibGalaxyTitles_ObjectPlayerId = NullId;
			
			// Get the target position of an object
			declare Position = Player.Position;
			for (I, 0, 2) Position[I] += C_LibGalaxyTitles_ObjectsDropSpreadSize[I] * ML::Rand(-1., 1.);
			
			// Create weapon marker
			if (C_LibGalaxyTitles_WeaponItemsTags.exists(MapLandmark.Tag)) {
				declare MarkerTag = "Object#"^MapLandmark.Id;
				MiniMap::SetMarker(
					MarkerTag, C_ImgBase^"Minimap/"^MapLandmark.Tag^".png",
					C_LibGalaxyTitles_MinimapColors[MapLandmark.Tag], Position
				);
				G_LibGalaxyTitles_MinimapMarkers.add(MarkerTag);
			}
			
			// Save object Id in dropped objects array
			declare SpawnTime = Now;
			while (G_LibGalaxyTitles_DroppedObjects.existskey(SpawnTime)) SpawnTime += 1;
			G_LibGalaxyTitles_DroppedObjects[SpawnTime] = Object.Id;
			
			// Spawn object
			Object.SetPosition(Position);
			G_LibGalaxyTitles_CreatedObjects.add(Object.Id);
		}
	}
	
	// ---------------------------------- //
	// Get the times out objects
	declare Ident[] ObjectsToDestroy;
	foreach (SpawnTime => ObjectId in G_LibGalaxyTitles_DroppedObjects)
		if (Now > SpawnTime + G_LibGalaxyTitles_ObjectsDropLifetime) ObjectsToDestroy.add(ObjectId);
	
	// ---------------------------------- //
	// Destroy timed out dropped objects
	foreach (ObjectId in ObjectsToDestroy) {
		if (!Objects.existskey(ObjectId)) continue;
		declare Object <=> Objects[ObjectId];
		declare Ident LibGalaxyTitles_AnchorId for Object;
		
		// ---------------------------------- //
		// Remove object from custom arrays
		declare Boolean Removed;
		Removed = G_LibGalaxyTitles_CreatedObjects.remove(ObjectId);
		Removed = G_LibGalaxyTitles_DroppedObjects.remove(ObjectId);
		
		// ---------------------------------- //
		// Update landmark information
		if (MapLandmarks_ObjectAnchor.existskey(LibGalaxyTitles_AnchorId)) {
			declare MapLandmark <=> MapLandmarks_ObjectAnchor[LibGalaxyTitles_AnchorId];
			declare Boolean LibGalaxyTitles_ObjectSpawned for MapLandmark;
			LibGalaxyTitles_ObjectSpawned = False;
			
			// ---------------------------------- //
			// Destroy weapon marker
			declare MarkerTag = "Object#"^LibGalaxyTitles_AnchorId;
			MiniMap::DestroyMarker(MarkerTag);
			Removed = G_LibGalaxyTitles_MinimapMarkers.remove(MarkerTag);
		}
		
		// Destroy object
		Object.SetUnspawned();
		ObjectDestroy(Object);
	}
	
	// ---------------------------------- //
	// Spawn objects on landmarks
	foreach (MapLandmark in MapLandmarks_ObjectAnchor) {
		declare LibGalaxyTitles_ObjectSpawned for MapLandmark = False;
		declare LibGalaxyTitles_ObjectNextSpawn for MapLandmark = Now;
		declare LibGalaxyTitles_ObjectPlayerId for MapLandmark = NullId;
		
		// ---------------------------------- //
		// Continue if landmark has object, the object is dropped somewhere else or object can't be spawned
		if (LibGalaxyTitles_ObjectSpawned || LibGalaxyTitles_ObjectNextSpawn > Now) continue;
		
		// ---------------------------------- //
		// Continue if landmark spawns weapons when they're not available
		if (!G_LibGalaxyTitles_WeaponItemsAvailable && C_LibGalaxyTitles_WeaponItemsTags.exists(MapLandmark.Tag)) continue;
		
		// ---------------------------------- //
		// Continue if weapon is carried by the player
		if (
			LibGalaxyTitles_ObjectPlayerId != NullId &&
			Players.existskey(LibGalaxyTitles_ObjectPlayerId) &&
			Players[LibGalaxyTitles_ObjectPlayerId].SpawnStatus == CSmPlayer::ESpawnStatus::Spawned
		) continue;
		
		// ---------------------------------- //
		// Create object
		declare CSmObject Object;
		switch (MapLandmark.Tag) {
			case "Armor"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Armor"]);
			case "Laser"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Laser"]);
			case "Nucleus"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Nucleus"]);
			case "Arrow"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Arrow"]);
			case "Rocket"	: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Rocket"]);
			case "Fuel"		: Object = ObjectCreate(G_LibGalaxyTitles_Objects["Fuel"]);
		}
		
		if (Object == Null) continue;
		LibGalaxyTitles_ObjectSpawned = True;
		
		// ---------------------------------- //
		// Set object properties
		declare Ident LibGalaxyTitles_AnchorId for Object;
		declare Text LibGalaxyTitles_Tag for Object;
		LibGalaxyTitles_AnchorId = MapLandmark.Id;
		LibGalaxyTitles_Tag = MapLandmark.Tag;
		
		// Reset weapon carrier
		LibGalaxyTitles_ObjectPlayerId = NullId;
		
		// Create weapon marker
		if (C_LibGalaxyTitles_WeaponItemsTags.exists(MapLandmark.Tag)) {
			declare MarkerTag = "Object#"^MapLandmark.Id;
			MiniMap::SetMarker(
				MarkerTag, C_ImgBase^"Minimap/"^MapLandmark.Tag^".png",
				C_LibGalaxyTitles_MinimapColors[MapLandmark.Tag], MapLandmark.Position
			);
			G_LibGalaxyTitles_MinimapMarkers.add(MarkerTag);
		}
		
		// Spawn object
		Object.SetPosition(MapLandmark.Position);
		G_LibGalaxyTitles_CreatedObjects.add(Object.Id);
	}
}

// ---------------------------------- //
/// Spawn objects on landmarks
Void SpawnObjects() { SpawnObjects(True); }

// ---------------------------------- //
/** Pick up object
 *
 *	@param	_Event			The event to proceed
 *	@param	_RespawnTime	Time before object will be spawned again after pick
 *
 *	@return		True, if the item is handled by the library
 */
Boolean PickUpObject(CSmModeEvent _Event, Integer _RespawnTime) {
	if (
		_Event == Null || _Event.Type != CSmModeEvent::EType::OnPlayerTouchesObject ||
		_Event.Object == Null || _Event.Player == Null || _Event.Player.IsBot ||
		_Event.Player.SpawnStatus != CSmPlayer::ESpawnStatus::Spawned ||
		!G_LibGalaxyTitles_LibraryLoaded
	) return False;
	
	declare Text LibGalaxyTitles_Tag for _Event.Object;
	declare Ident LibGalaxyTitles_AnchorId for _Event.Object;
	
	// Return False if object is not handled by GalaxyTitles library
	if (!C_LibGalaxyTitles_AcceptedObjects.exists(LibGalaxyTitles_Tag)) return False;
	
	Discard(_Event);
	
	// ---------------------------------- //
	// Pick conditions
	switch (LibGalaxyTitles_Tag) {
		// ---------------------------------- //
		// Armor conditions
		case "Armor" : {
			declare CanPickUpArmor = _Event.Player.Armor < _Event.Player.ArmorMax;
			
			// Check if players nearby can be healed
			if (!CanPickUpArmor) foreach (Player in Players)
				if (
					Player != _Event.Player && Player.Armor < Player.ArmorMax &&
					ML::Distance(_Event.Player.Position, Player.Position) <= C_LibGalaxyTitles_ArmorPickupRadius &&
					((_Event.Player.CurrentClan == 0 || Player.CurrentClan != _Event.Player.CurrentClan) &&
					!G_LibGalaxyTitles_ObjectsAffectEveryone)
				) {
					CanPickUpArmor = True;
					break;
				}
			
			// Cant pick up Armor
			if (!CanPickUpArmor) return True;
		}
	}
	
	// ---------------------------------- //
	// Item respawn timer
	if (MapLandmarks_ObjectAnchor.existskey(LibGalaxyTitles_AnchorId)) {
		declare MapLandmark <=> MapLandmarks_ObjectAnchor[LibGalaxyTitles_AnchorId];
		declare LibGalaxyTitles_ObjectSpawned for MapLandmark = False;
		declare LibGalaxyTitles_ObjectNextSpawn for MapLandmark = Now;
		declare LibGalaxyTitles_ObjectPlayerId for MapLandmark = NullId;
		
		LibGalaxyTitles_ObjectSpawned = False;
		LibGalaxyTitles_ObjectNextSpawn = Now + _RespawnTime;
		
		// ---------------------------------- //
		// Set weapon carrier
		if (C_LibGalaxyTitles_WeaponItemsTags.exists(MapLandmark.Tag)) {
			LibGalaxyTitles_ObjectNextSpawn = Now;
			LibGalaxyTitles_ObjectPlayerId = _Event.Player.Id;
			
			// ---------------------------------- //
			// Destroy weapon marker
			declare MarkerTag = "Object#"^MapLandmark.Id;
			MiniMap::DestroyMarker(MarkerTag);
			declare Removed = G_LibGalaxyTitles_MinimapMarkers.remove(MarkerTag);
		}
	}
	
	// ---------------------------------- //
	// Apply effect
	declare NbOtherPlayersAffected = 0;
	declare Message = Text[];
	
	switch (LibGalaxyTitles_Tag) {
		// ---------------------------------- //
		// Armor - heal players
		case "Armor" : {
			foreach (Player in Players) {
				if (
					Player.Armor >= Player.ArmorMax ||
					ML::Distance(_Event.Player.Position, Player.Position) > C_LibGalaxyTitles_FuelPickupRadius ||
					((_Event.Player.CurrentClan == 0 || Player.CurrentClan != _Event.Player.CurrentClan) &&
					!G_LibGalaxyTitles_ObjectsAffectEveryone)
				)
					continue;
				
				// ---------------------------------- //
				// Heal player
				AddPlayerArmor(Player, C_LibGalaxyTitles_ArmorHealAmount, Null, 0);
				
				// ---------------------------------- //
				// Notify player about who healed them
				if (Player == _Event.Player) continue;
				NbOtherPlayersAffected += 1;
				
				Interfancy::SendCrosshairNote(
					Player, ["$<%1$> healed you!", _Event.Player.Name],
					C_LibGalaxyTitles_NoteDisplayTime
				);
			}
			
			// ---------------------------------- //
			// Notify player how many teammates they've healed
			if (NbOtherPlayersAffected > 0) {
				if (NbOtherPlayersAffected > 1) Message = ["Healed %1 players", TL::ToText(NbOtherPlayersAffected)];
				else Message = ["Healed 1 player"];
				Statistics::TrackEvent(_Event.Player, "PlayersHealed", NbOtherPlayersAffected);
			}
		}
		// ---------------------------------- //
		// Fuel - reload boost and stamina
		case "Fuel" : {
			foreach (Player in Players) {
				if (
					ML::Distance(_Event.Player.Position, Player.Position) > C_LibGalaxyTitles_FuelPickupRadius ||
					((_Event.Player.CurrentClan == 0 || Player.CurrentClan != _Event.Player.CurrentClan) &&
					!G_LibGalaxyTitles_ObjectsAffectEveryone)
				)
					continue;
				
				// ---------------------------------- //
				// Boost player
				AddReloadBoost(Player);
				Player.Stamina = ML::FloorInteger(Player.StaminaMax * 3600);
				
				// ---------------------------------- //
				// Notify player about who boosted them
				if (Player == _Event.Player) continue;
				NbOtherPlayersAffected += 1;
				
				Interfancy::SendCrosshairNote(
					Player, ["$<%1$> boosted you!", _Event.Player.Name],
					C_LibGalaxyTitles_NoteDisplayTime
				);
			}
			
			// ---------------------------------- //
			// Notify player how many teammates they've boosted
			if (NbOtherPlayersAffected > 0) {
				if (NbOtherPlayersAffected > 1) Message = ["Boosted %1 players", TL::ToText(NbOtherPlayersAffected)];
				else Message = ["Boosted 1 player"];
				Statistics::TrackEvent(_Event.Player, "PlayersBoosted", NbOtherPlayersAffected);
			}
		}
		// ---------------------------------- //
		// Give Laser weapon slot
		case "Laser" : Inventory::AddWeaponSlots(_Event.Player, CSmMode::EWeapon::Laser, 1);
		
		// ---------------------------------- //
		// Give Nucleus weapon slot
		case "Nucleus" : Inventory::AddWeaponSlots(_Event.Player, CSmMode::EWeapon::Nucleus, 1);
		
		// ---------------------------------- //
		// Give Arrow weapon slot
		case "Arrow" : Inventory::AddWeaponSlots(_Event.Player, CSmMode::EWeapon::Arrow, 1);
		
		// ---------------------------------- //
		// Give Rocket weapon slot
		case "Rocket" : Inventory::AddWeaponSlots(_Event.Player, CSmMode::EWeapon::Rocket, 1);
	}
	
	// ---------------------------------- //
	// Send notice and give points if player has affected somebody
	if (NbOtherPlayersAffected > 0) {
		Interfancy::SendCrosshairNote(_Event.Player, Message, C_LibGalaxyTitles_NoteDisplayTime);
		Score::AddPoints(_Event.Player, NbOtherPlayersAffected * G_LibGalaxyTitles_PointsForPick);
		TopScores::AddPoints(_Event.Player.Score, "Items", NbOtherPlayersAffected * G_LibGalaxyTitles_PointsForPick);
	}
	
	if (C_LibGalaxyTitles_WeaponItemsTags.exists(LibGalaxyTitles_Tag)) {
		// ---------------------------------- //
		// Crosshair notice on weapon added
		Interfancy::SendCrosshairNote(_Event.Player, ["+1 %1", LibGalaxyTitles_Tag], C_LibGalaxyTitles_NoteDisplayTime);
		
		// ---------------------------------- //
		// Add weapon to drop
		declare Ident[] PlayerWeaponsToDrop for _Event.Player;
		if (!PlayerWeaponsToDrop.exists(LibGalaxyTitles_AnchorId)) PlayerWeaponsToDrop.add(LibGalaxyTitles_AnchorId);
	}
	
	// ---------------------------------- //
	// Destroy object (unspawning the object gives a quiet sound)
	declare Removed = G_LibGalaxyTitles_CreatedObjects.remove(_Event.Object.Id);
	_Event.Object.SetUnspawned();
	ObjectDestroy(_Event.Object);
	return True;
}

Boolean PickUpObject(CSmModeEvent _Event) {
	return PickUpObject(_Event, C_LibGalaxyTitles_ObjectsRespawnTime);
}

// ---------------------------------- //
/** Initialize Toads spawn landmarks
 *
 *	@param	_ToadsSpawnRatio	Target ratio of landmarks spawning Toads to all landmarks
 */
Void InitToads(Real _ToadsSpawnRatio) {
	DestroyAllToads();
	G_LibGalaxyTitles_ToadsSpawns.clear();
	if (MapLandmarks_BotPath.count <= 0 || _ToadsSpawnRatio <= 0 || !G_LibGalaxyTitles_LibraryLoaded) return;
	
	// ---------------------------------- //
	// Reset Toads information in landmarks
	foreach (MapLandmark in MapLandmarks_BotPath) {
		declare Boolean LibGalaxyTitles_ToadSpawned for MapLandmark;
		declare Integer LibGalaxyTitles_ToadNextSpawn for MapLandmark;
		LibGalaxyTitles_ToadSpawned = False;
		LibGalaxyTitles_ToadNextSpawn = Now;
	}
	
	// ---------------------------------- //
	// Select landmark what will spawn Toads
	declare LandmarksRatio = 0.;
	while (True) {
		declare Ident LandmarkId;
		LandmarkId = NullId;
		
		while (True) {
			LandmarkId = MapLandmarks_BotPath[ML::Rand(0, MapLandmarks_BotPath.count - 1)].Id;
			if (!G_LibGalaxyTitles_ToadsSpawns.exists(LandmarkId)) break;
		}
		
		G_LibGalaxyTitles_ToadsSpawns.add(LandmarkId);
		LandmarksRatio = ML::ToReal(G_LibGalaxyTitles_ToadsSpawns.count) / MapLandmarks_BotPath.count;
		if (LandmarksRatio >= _ToadsSpawnRatio || G_LibGalaxyTitles_ToadsSpawns.count >= MapLandmarks_BotPath.count) break;
	}
}

// ---------------------------------- //
/// Initialize Toads spawn landmarks with default spawn ratio 
Void InitToads() { InitToads(C_LibGalaxyTitles_ToadLandmarksRatio); }

// ---------------------------------- //
/** Manage everything related to Toads
 *
 *	@param	_ToadsRespawnTime	Time before Toad will respawn after elimination
 */
Void ToadsLoop(Integer _ToadsRespawnTime) {
	if (!G_LibGalaxyTitles_LibraryLoaded) return;
	
	// ---------------------------------- //
	// Spawn Toads
	foreach (MapLandmark in MapLandmarks_BotPath) {
		if (_ToadsRespawnTime <= 0 || !G_LibGalaxyTitles_ToadsSpawns.exists(MapLandmark.Id)) continue;
		
		declare LibGalaxyTitles_ToadSpawned for MapLandmark = False;
		declare LibGalaxyTitles_ToadNextSpawn for MapLandmark = Now;
		
		// ---------------------------------- //
		// Continue if landmark can't spawn Toad or if Toad is already spawned
		if (LibGalaxyTitles_ToadSpawned || LibGalaxyTitles_ToadNextSpawn > Now) continue;
		
		// ---------------------------------- //
		// Create Toad
		declare Toad <=> CreateBotPlayer(G_LibGalaxyTitles_Objects["Toad"], 0);
		
		// ---------------------------------- //
		// Define behaviour depending on amount of path points
		if (MapLandmark.BotPath.Path.count > 1) {
			Toad.Driver.Behaviour = CSmPlayerDriver::ESmDriverBehaviour::Patrol;
			if (
				MapLandmark.BotPath.Path.count >= 3 &&
				ML::Distance(MapLandmark.BotPath.Path[0], MapLandmark.BotPath.Path[MapLandmark.BotPath.Path.count-1]) <= 8.
			)
				Toad.Driver.Patrol_Mode = CSmPlayerDriver::ESmDriverPatrolMode::Loop;
			else
				Toad.Driver.Patrol_Mode = CSmPlayerDriver::ESmDriverPatrolMode::BackAndForth;
		} else
			Toad.Driver.Behaviour = CSmPlayerDriver::ESmDriverBehaviour::Saunter;
		
		// ---------------------------------- //
		// Configure Toad
		Toad.Driver.AttackFilter = CSmPlayerDriver::ESmAttackFilter::Nobody;
		Toad.Driver.Saunter_BaseChillingTime	= C_LibGalaxyTitles_ToadChillingTime;
		Toad.Driver.Saunter_ChillingTimeDelta	= 0;
		Toad.Driver.Saunter_Radius	= C_LibGalaxyTitles_ToadSaunterRadius;
		Toad.Driver.ShootPeriodMin	= C_LibGalaxyTitles_ToadShootPeriod;
		Toad.Driver.ShootPeriodMax	= C_LibGalaxyTitles_ToadShootPeriod;
		Toad.Driver.AggroRadius		= C_LibGalaxyTitles_ToadPeaceDistance;
		Toad.Driver.DisengageDistance	= C_LibGalaxyTitles_ToadPeaceDistance;
		Toad.SpeedPower	= C_LibGalaxyTitles_ToadSpeedPower;
		Toad.AmmoGain	= C_LibGalaxyTitles_ToadAmmoGain;
		Toad.ArmorMax	= G_LibGalaxyTitles_ToadsArmorMax;
		Toad.ForceColor	= Private_GetThirdClanColor(Teams[0].ColorPrimary, Teams[1].ColorPrimary);
		
		// ---------------------------------- //
		// Spawn Toad
		declare Ident LibGalaxyTitles_AnchorId for Toad;
		LibGalaxyTitles_AnchorId = MapLandmark.Id;
		
		SpawnBotPlayer(Toad, 0, Toad.ArmorMax, MapLandmark.BotPath, Now);
		G_LibGalaxyTitles_SpawnedToads.add(Toad.Id);
		LibGalaxyTitles_ToadSpawned = True;
	}
	
	// ---------------------------------- //
	// Manage events
	foreach (Event in PendingEvents) {
		// ---------------------------------- //
		// Respawn eliminated Toads
		if (Event.Type == CSmModeEvent::EType::OnArmorEmpty) {
			if (Event.Victim != Null && G_LibGalaxyTitles_SpawnedToads.exists(Event.Victim.Id)) {
				declare Ident LibGalaxyTitles_AnchorId for Event.Victim;
				if (MapLandmarks_BotPath.existskey(LibGalaxyTitles_AnchorId)) {
					declare MapLandmark <=> MapLandmarks_BotPath[LibGalaxyTitles_AnchorId];
					declare Boolean LibGalaxyTitles_ToadSpawned for MapLandmark = False;
					declare Integer LibGalaxyTitles_ToadNextSpawn for MapLandmark = Now;
					LibGalaxyTitles_ToadSpawned = False;
					LibGalaxyTitles_ToadNextSpawn = Now + _ToadsRespawnTime;
				}
				
				// Destroy Toad
				declare Removed = G_LibGalaxyTitles_SpawnedToads.remove(Event.Victim.Id);
				DestroyBotPlayer(Event.Victim);
			}
		}
		// ---------------------------------- //
		// Make player who shoot the Toad its target
		else if (Event.Type == CSmModeEvent::EType::OnHit) {
			if (Event.Victim != Null && G_LibGalaxyTitles_SpawnedToads.exists(Event.Victim.Id))
				Event.Victim.Driver.ForcedTarget = Event.Shooter;
		}
		// ---------------------------------- //
		// Make the Player shooting near Toad its target
		else if (Event.Type == CSmModeEvent::EType::OnShoot) {
			if (Event.Shooter != Null && !Event.Shooter.IsBot) {
				foreach (BotPlayer in BotPlayers) {
					if (
						G_LibGalaxyTitles_SpawnedToads.exists(BotPlayer.Id) &&
						ML::Distance(BotPlayer.Position, Event.Shooter.Position) <= C_LibGalaxyTitles_ToadAggroRadius
					)
						BotPlayer.Driver.ForcedTarget = Event.Shooter;
				}
			}
		}
		// ---------------------------------- //
		// Attack the Player if missed a Toad
		else if (Event.Type == CSmModeEvent::EType::OnNearMiss) {
			if (Event.Victim != Null && G_LibGalaxyTitles_SpawnedToads.exists(Event.Victim.Id))
				Event.Victim.Driver.ForcedTarget = Event.Shooter;
		}
	}
	
	// ---------------------------------- //
	// Manage Toads functions
	foreach (BotPlayer in BotPlayers) {
		if (!G_LibGalaxyTitles_SpawnedToads.exists(BotPlayer.Id)) continue;
		
		// ---------------------------------- //
		// Make player touching Toad its target
		if (BotPlayer.Driver.ForcedTarget == Null) {
			foreach (Player in Players) {
				if (
					Player.Armor > 0 && Player.SpawnStatus == CSmPlayer::ESpawnStatus::Spawned &&
					ML::Distance(BotPlayer.Position, Player.Position) < 1.5
				)
					BotPlayer.Driver.ForcedTarget = Player;
			}
		}
		
		// ---------------------------------- //
		// Clear Toad target if out of range
		if (BotPlayer.Driver.ForcedTarget != Null) {
			declare Player <=> BotPlayer.Driver.ForcedTarget;
			if (
				Player.Armor <= 0 || Player.SpawnStatus != CSmPlayer::ESpawnStatus::Spawned ||
				ML::Distance(BotPlayer.Position, Player.Position) > BotPlayer.Driver.DisengageDistance
			)
				BotPlayer.Driver.ForcedTarget = Null;
		}
	}
	
	// ---------------------------------- //
	// Update Toads colour on team colour change
	foreach (I => Team in Teams) if (
		!G_LibGalaxyTitles_PrevTeamsColors.existskey(I) ||
		G_LibGalaxyTitles_PrevTeamsColors[I] != Team.ColorPrimary
	) {
		G_LibGalaxyTitles_PrevTeamsColors[I] = Team.ColorPrimary;
		declare NewToadsColor = Private_GetThirdClanColor(Teams[0].ColorPrimary, Teams[1].ColorPrimary);
		foreach (BotPlayer in BotPlayers) BotPlayer.ForceColor = NewToadsColor;
	}
}

// ---------------------------------- //
/// Manage everything related to Toads
Void ToadsLoop() { ToadsLoop(C_LibGalaxyTitles_ToadRespawnTime); }

// ---------------------------------- //
/** Initialize the library
 *
 *	@param	_UseWeapons			Combo weapon items usage
 *	@param	_ToadsSpawnRatio	Target ratio of landmarks spawning Toads to all landmarks
 */
Void Init(Boolean _UseWeapons, Real _ToadsSpawnRatio) {
	ClearReloadBoost();
	InitObjects(_UseWeapons);
	InitToads(_ToadsSpawnRatio);
}

Void Init(Boolean _UseWeapons) {
	ClearReloadBoost();
	InitObjects(_UseWeapons);
	InitToads();
}

Void Init(Real _ToadsSpawnRatio) {
	ClearReloadBoost();
	InitObjects();
	InitToads(_ToadsSpawnRatio);
}

Void Init() {
	ClearReloadBoost();
	InitObjects();
	InitToads();
}

// ---------------------------------- //
/** Automatic library loop
 *
 *	@param	_SpawnObjects		If False, objects won't be spawned
 *	@param	_ToadsRespawnTime	Time before Toad will respawn after elimination
 */
Void Loop(Boolean _SpawnObjects, Integer _ToadsRespawnTime) {
	ReloadBoost();
	SpawnObjects(_SpawnObjects);
	ToadsLoop(_ToadsRespawnTime);
}

Void Loop(Boolean _SpawnObjects) {
	ReloadBoost();
	SpawnObjects(_SpawnObjects);
	ToadsLoop();
}

Void Loop(Integer _ToadsRespawnTime) {
	ReloadBoost();
	SpawnObjects();
	ToadsLoop(_ToadsRespawnTime);
}

Void Loop() {
	ReloadBoost();
	SpawnObjects();
	ToadsLoop();
}

// ---------------------------------- //
/** Reset and load player weapons after player is respawned
 *	/!\ Must be placed after player is spawned in the world /!\
 *
 *	@param	_Player		Player to set weapon
 */
Void InitPlayerInventory(CSmPlayer _Player) {
	if (_Player == Null || !G_LibGalaxyTitles_LibraryLoaded) return;
	Inventory::ResetInventory(_Player);
	
	if (!G_LibGalaxyTitles_WeaponItemsAvailable) return;
	
	// ---------------------------------- //
	// Get the available weapons
	declare PlayerWeapons = [CSmMode::EWeapon::Rocket];
	declare TextToWeapon = [
		"Laser"		=> CSmMode::EWeapon::Laser,
		"Nucleus"	=> CSmMode::EWeapon::Nucleus,
		"Arrow"		=> CSmMode::EWeapon::Arrow
	];
	
	foreach (WeaponName in G_LibGalaxyTitles_AvailableWeapons) {
		if (!TextToWeapon.existskey(WeaponName)) continue;
		declare Weapon = TextToWeapon[WeaponName];
		if (!PlayerWeapons.exists(Weapon)) PlayerWeapons.add(Weapon);
	}
	
	Inventory::SetAvailableWeapons(_Player, PlayerWeapons);
	
	// ---------------------------------- //
	// Init weapons
	Inventory::SetWeaponSlots(_Player, CSmMode::EWeapon::Rocket, 4);
	Inventory::SetFirstWeapon(_Player);
	
	// Reset weapons to drop
	declare Ident[] PlayerWeaponsToDrop for _Player;
	PlayerWeaponsToDrop.clear();
	
	// Change icons to GalaxyTitles style
	foreach (Weapon in C_LibGalaxyTitles_WeaponItemsTags)
		Inventory::ChangeSlotIcon(_Player, "LibInventory_Weapon"^Weapon, C_ImgBase^"Icons/"^Weapon^".png");
}

// ---------------------------------- //
/** Initialize all settings of a player
 *
 *	@param	_Player		Player to initialize
 */
Void InitPlayer(CSmPlayer _Player) {
	if (_Player == Null || !G_LibGalaxyTitles_LibraryLoaded) return;
	ClearReloadBoost(_Player);
	InitPlayerInventory(_Player);
}

// ---------------------------------- //
/** Set if objects affect also enemy players
 *
 *	@param	_Setting	True, if objects may affect enemies
 */
Void SetObjectsAffectEveryone(Boolean _Setting) {
	G_LibGalaxyTitles_ObjectsAffectEveryone = _Setting;
}

// ---------------------------------- //
/** Set amount of points given for picking up object
 *
 *	@param	_Setting	Amount of points for picking up object
 */
Void SetPointsForPick(Integer _Setting) {
	G_LibGalaxyTitles_PointsForPick = _Setting;
}

// ---------------------------------- //
/** Set amount of Toads armor points
 *
 *	@param	_Setting	Toads armor points
 */
Void SetToadsArmorMax(Integer _Setting) {
	G_LibGalaxyTitles_ToadsArmorMax = _Setting;
}

// ---------------------------------- //
/** Set the time before dropped weapons reappear in spawners
 *
 *	@param	_Setting	Respawn duration
 */
Void SetDroppedWeaponsLifetime(Integer _Setting) {
	G_LibGalaxyTitles_ObjectsDropLifetime = _Setting;
}