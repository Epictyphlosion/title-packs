// -------------------------------------- //
//  LIBRARY PAUSE MENU by domino54        //
//  script version: 2017-03-29            //
// -------------------------------------- //

/**
 *	Works only in the title pack "GalaxyTitles@domino54".
 *	This library creates customized pause menu, stylized
 *	for the GalaxyTitles title pack. Main window and team
 *	selection are replaced, mode can also display rules
 *	reminder when new player plays the mode for the first
 *	time.
 */

#Const Version		"2017-03-29"
#Const ScriptName	"PauseMenu.Script.txt"

#Include "MathLib" as ML
#Include "Libs/Nadeo/Layers2.Script.txt" as Layers
#Include "Libs/Nadeo/Manialink.Script.txt" as Manialink
#Include "Libs/domino54/ManialinkTools.Script.txt" as ManialinkTools
#Include "Libs/domino54/Translations.Script.txt" as Translations

// ---------------------------------- //
// Constants
// ---------------------------------- //
#Const C_ImgBase "file://Media/Manialinks/ShootMania/GalaxyTitles/"

// ---------------------------------- //
// Global variables
// ---------------------------------- //
declare Integer[Ident] G_LibPauseMenu_TeamChangeRequests;

// ---------------------------------- //
// Functions
// ---------------------------------- //

// ---------------------------------- //
// Private
// ---------------------------------- //

// ---------------------------------- //
/** Create manialink for the pause menu layer
 *
 *	@return		The pause menu layer
 */
Text Private_CreateManialinkPauseMenu() {
	declare netwrite Text Net_LibPauseMenu_HelpModeObjectives for Teams[0];
	return """
<manialink version="3" name="LibPauseMenu:Pause">
<stylesheet>
	<style class="buttonFont" halign="center" valign="center2" textsize="2" textcolor="BBB" textfont="Eurostile" textprefix="$t" />
</stylesheet>

<!-- Background blur -->
<quad z-index="20" size="320 180" halign="center" valign="center" style="Bgs1" substyle="BgDialogBlur" />

<!-- Pause menu window -->
<frame id="Frame_PauseWindow" pos="-30 32" z-index="21">
	<!-- Title block -->
	<frame pos="30 -5">
		<label
			size="50" halign="center" valign="center2" textsize="2" textcolor="000"
			textfont="Eurostile_Bold" textprefix="$t" text="Pause" translate="1"
		/>
		<quad size="59 9" halign="center" valign="center" bgcolor="FFF" />
	</frame>

	<!-- Pause menu button model -->
	<framemodel id="FrameModel_PauseButton">
		<label pos="30 -3.5" size="52" class="buttonFont" id="Label_ButtonText" />
		<quad pos="58 -3.5" size="3 3" halign="right" valign="center" colorize="AAA" id="Quad_ButtonCheck" />
		<quad size="60 7" class="hoverButton colorizable" opacity="0" ScriptEvents="1" />
	</framemodel>
	
	<!-- Pause buttons -->
	<frame id="Frame_PauseButtons" pos="0 -11">
		<frameinstance modelid="FrameModel_PauseButton" id="Frame_PauseButton#0" hidden="1" />
		<frameinstance modelid="FrameModel_PauseButton" id="Frame_PauseButton#1" hidden="1" />
		<frameinstance modelid="FrameModel_PauseButton" id="Frame_PauseButton#2" hidden="1" />
		<frameinstance modelid="FrameModel_PauseButton" id="Frame_PauseButton#3" hidden="1" />
		<frameinstance modelid="FrameModel_PauseButton" id="Frame_PauseButton#4" hidden="1" />
		<frameinstance modelid="FrameModel_PauseButton" id="Frame_PauseButton#5" hidden="1" />
	</frame>
	
	<!-- Resume and quit buttons -->
	<frame id="Frame_ButtonResume" pos="6 -54">
		<label pos="12 -4" class="buttonFont" text="Resume" translate="1" id="Label_ButtonText" />
		<quad size="24 8" class="hoverButton colorizable" opacity="0" id="Quad_ButtonResume" ScriptEvents="1" />
	</frame>
	<frame id="Frame_ButtonQuit" pos="30 -54">
		<label pos="12 -4" class="buttonFont" text="Quit" translate="1" id="Label_ButtonText" />
		<quad size="24 8" class="hoverButton colorizable" opacity="0" id="Quad_ButtonQuit" ScriptEvents="1" />
	</frame>

	<quad size="60 64" bgcolor="000" opacity=".75" ScriptEvents="1" />
	<quad size="60 64" style="Bgs1" substyle="BgDialogBlur" />
	<quad pos="-2.5 2.5" size="130 138" scale=".5" style="Bgs1InRace" substyle="BgButtonShadow" />
</frame>

<!-- Team selection window -->
<frame id="Frame_TeamSelection" pos="0 36" z-index="21" hidden="1">
	<!-- Title block -->
	<frame pos="0 -5">
		<label
			size="90" halign="center" valign="center2" textsize="2" textcolor="000"
			textfont="Eurostile_Bold" textprefix="$t" text="Choose your team" translate="1"
		/>
		<quad size="99 9" halign="center" valign="center" bgcolor="FFF" id="Quad_TeamsHeaderBg" />
	</frame>

	<!-- Team card frame model -->
	<framemodel id="FrameModel_TeamCard">
		<quad pos="0 -16 .1" size="18 18" halign="center" valign="center" style="Emblems" substyle="#0" id="Quad_TeamEmblem" />
		<label
			pos="0 -28 .1" size="44" halign="center" valign="center2" id="Label_TeamName"
			textemboss="1" textcolor="FFF" textfont="Eurostile"
		/>
		<label
			pos="0 -33 .1" halign="center" valign="center2" id="Label_TeamNbPlayers"
			textsize="1" textemboss="1" textcolor="FFF" textfont="Eurostile_Bold"
		/>
		<label
			pos="0 -40 .1" halign="center" valign="center2" id="Label_JoinTeamText" opacity="0"
			textsize="2" textemboss="1" textcolor="FFF" textfont="Eurostile_Bold" textprefix="$t" text="Join" translate="1"
		/>
		<quad halign="center" size="49 49" opacity=".125" id="Quad_CardBackground" ScriptEvents="1" />
	</framemodel>
	
	<!-- Teams cards -->
	<frame id="Frame_TeamsCardsCon" pos="0 -11">
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#0" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#1" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#2" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#3" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#4" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#5" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#6" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#7" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#8" />
		<frameinstance modelid="FrameModel_TeamCard" id="Frame_TeamCard#9" />
	</frame>
	
	<!-- Return button -->
	<frame id="Frame_TeamsButtonReturn" pos="0 -62">
		<label pos="0 -4" class="buttonFont" text="Return" translate="1" id="Label_ButtonText" />
		<quad size="48 8" halign="center" class="hoverButton colorizable" opacity="0" id="Quad_TeamsButtonReturn" ScriptEvents="1" />
	</frame>

	<quad size="100 72" halign="center" bgcolor="000" opacity=".75" ScriptEvents="1" id="Quad_TeamsWindowBg" />
	<quad size="100 72" halign="center" style="Bgs1" substyle="BgDialogBlur" id="Quad_TeamsWindowBlur" />
	<quad pos="0 2.5" size="210 154" halign="center" scale=".5" style="Bgs1InRace" substyle="BgButtonShadow" id="Quad_TeamsWindowShad" />
</frame>

<!-- Mode help window -->
<frame id="Frame_HelpWindow" pos="-70" z-index="21" hidden="1">
	<!-- Title block -->
	<frame pos="70 -5 1">
		<label
			size="130" halign="center" valign="center2"
			textsize="2" textcolor="000" textfont="Eurostile_Bold" textprefix="$t" id="Label_HelpModeName"
		/>
		<quad size="139 9" halign="center" valign="center" bgcolor="FFF" />
	</frame>
	
	<!-- Mode objectives -->
	<label
		pos="70" size="120" halign="center" id="Label_HelpModeObjectives"
		textsize="2" textcolor="FFF" textfont="Eurostile" autonewline="1"
	/>
	
	<!-- Resume button -->
	<frame id="Frame_ButtonHelpResume" pos="58 0 .1">
		<label pos="12 -4 .2" class="buttonFont" text="OK" id="Label_ButtonText" />
		<quad size="24 8" class="hoverButton colorizable" opacity="0" id="Quad_ButtonHelpResume" ScriptEvents="1" />
	</frame>

	<quad size="140" bgcolor="000" opacity=".75" ScriptEvents="1" id="Quad_HelpBgColor" />
	<quad size="140" style="Bgs1" substyle="BgDialogBlur" id="Quad_HelpBgBlur" />
	<quad pos="-2.5 2.5" size="290" scale=".5" style="Bgs1InRace" substyle="BgButtonShadow" id="Quad_HelpBgShadow" />
</frame>

<script><!--
/**
 *	LibPauseMenu: Pause
 */

#Include "MathLib" as ML
#Include "TextLib" as TL

// ---------------------------------- //
// Constants
// ---------------------------------- //
#Const C_DefaultInterfaceColor <1., .875, 0.>
#Const C_LabelsValues [_("Help"), _("Spectator"), _("Change team"), _("Save replay"), _("Manage server"), _("Advanced options")]

// ---------------------------------- //
// Global variables
// ---------------------------------- //
declare Vec3 G_InterfaceColor;

// ---------------------------------- //
// Main
// ---------------------------------- //
***Main***
***
// ---------------------------------- //
// Load interface elements
declare Frame_PauseWindow <=> (Page.GetFirstChild("Frame_PauseWindow") as CMlFrame);
declare Frame_TeamSelection <=> (Page.GetFirstChild("Frame_TeamSelection") as CMlFrame);
declare Frame_HelpWindow <=> (Page.GetFirstChild("Frame_HelpWindow") as CMlFrame);

declare CMlFrame[] Frame_PauseButtons;
for (I, 0, 5) Frame_PauseButtons.add((Page.GetFirstChild("Frame_PauseButton#"^I) as CMlFrame));

// ---------------------------------- //
// Teams window
declare Frame_TeamsCardsCon <=> (Page.GetFirstChild("Frame_TeamsCardsCon") as CMlFrame);
declare Quad_TeamsWindowBg <=> (Page.GetFirstChild("Quad_TeamsWindowBg") as CMlQuad);
declare Quad_TeamsWindowBlur <=> (Page.GetFirstChild("Quad_TeamsWindowBlur") as CMlQuad);
declare Quad_TeamsWindowShad <=> (Page.GetFirstChild("Quad_TeamsWindowShad") as CMlQuad);
declare Quad_TeamsHeaderBg <=> (Page.GetFirstChild("Quad_TeamsHeaderBg") as CMlQuad);

declare CMlFrame[] Frame_TeamsCards;
for (I, 0, 9) Frame_TeamsCards.add((Page.GetFirstChild("Frame_TeamCard#"^I) as CMlFrame));

// ---------------------------------- //
// Help window
declare Quad_HelpBgColor <=> (Page.GetFirstChild("Quad_HelpBgColor") as CMlQuad);
declare Quad_HelpBgBlur <=> (Page.GetFirstChild("Quad_HelpBgBlur") as CMlQuad);
declare Quad_HelpBgShadow <=> (Page.GetFirstChild("Quad_HelpBgShadow") as CMlQuad);
declare Label_HelpModeName <=> (Page.GetFirstChild("Label_HelpModeName") as CMlLabel);
declare Label_HelpModeObjectives <=> (Page.GetFirstChild("Label_HelpModeObjectives") as CMlLabel);
declare Frame_ButtonHelpResume <=> (Page.GetFirstChild("Frame_ButtonHelpResume") as CMlFrame);

// ---------------------------------- //
// Get all controls that ban be colorized
declare CMlControl[] ColorizableControls;
Page.GetClassChildren("colorizable", Page.MainFrame, True);
foreach (Control in Page.GetClassChildren_Result) {
	declare TargetControl = Control;
	ColorizableControls.add(TargetControl);
}

// ---------------------------------- //
// Get all buttons
declare CMlFrame[] Frame_Buttons;
Frame_Buttons = Frame_PauseButtons;
Frame_Buttons.add((Page.GetFirstChild("Frame_ButtonResume") as CMlFrame));
Frame_Buttons.add((Page.GetFirstChild("Frame_ButtonQuit") as CMlFrame));
Frame_Buttons.add((Page.GetFirstChild("Frame_TeamsButtonReturn") as CMlFrame));
Frame_Buttons.add(Frame_ButtonHelpResume);

// ---------------------------------- //
// Persistent settings
declare persistent Text[] Persistent_LibPauseMenu_ModesWithRulesRead for LocalUser;

// ---------------------------------- //
// Netcode
declare netread Text Net_LibPauseMenu_HelpModeName for Teams[0];
declare netread Text Net_LibPauseMenu_HelpModeObjectives for Teams[0];
declare netread Text Net_LibPauseMenu_HelpModeImage for Teams[0];
declare netread Boolean Net_LibPauseMenu_IsSoloMode for Teams[0];

// ---------------------------------- //
// Custom teams
declare netread Integer Net_LibPauseMenu_CustomTeams for Teams[0];
declare netread Vec3[Integer] Net_LibPauseMenu_TeamsColors for Teams[0];
declare netread Text[Integer] Net_LibPauseMenu_TeamsNames for Teams[0];

// ---------------------------------- //
// Variables
declare UpdatePauseButtons = True;
declare ActionToTrigger = "";
declare UpdateWindowsVisibility = True;
declare ActiveSubWindow = "";
declare UpdateTeamsCards = True;
declare UpdateHelpWindow = 2;

/// Executable layer
declare Text[] LayerExecutableEvents for This;

/// Update
declare PrevPageIsVisible = False;
declare PrevInterfaceColor = Vec3;
declare PrevUseClans = False;
declare PrevUseForcedClans = False;
declare PrevIsSpectatorMode = False;
declare PrevTeamsColors = [0 => Vec3, 1 => Vec3];
declare PrevTeamsNames = [0 => "", 1 => ""];
declare PrevClansNbPlayers = [0 => 0, 1 => 0, 2 => 0];
declare PrevModeName = "";
declare PrevNbCustomTeams = 0;
declare PrevCustomTeamsNames = Text[Integer];
declare PrevCustomTeamsColors = Vec3[Integer];
declare PrevGUIPlayerHue = -1.;
declare PrevIsSoloMode = False;

// ---------------------------------- //
// Initialize
HideResumePlayingButton = True;
CloseInGameMenu(::EInGameMenuResult::Resume);
EnableMenuNavigation(True, False, Null, 0);
***

// ---------------------------------- //
// Yield
// ---------------------------------- //
***Yield***
***
// ---------------------------------- //
// Trigger page action in next tick after pause is hidden
if (!PageIsVisible && ActionToTrigger != "") {
	TriggerPageAction(ActionToTrigger);
	ActionToTrigger = "";
}

// ---------------------------------- //
// Manage events
foreach (Event in PendingEvents) {
	// ---------------------------------- //
	// On mouse click
	if (Event.Type == CMlEvent::Type::MouseClick) {
		// ---------------------------------- //
		// Resume game button
		if (Event.ControlId == "Quad_ButtonResume") {
			CloseInGameMenu(::EInGameMenuResult::Resume);
			(Event.Control as CMlQuad).Opacity = 0.;
		}
		
		// ---------------------------------- //
		// Leave server button
		else if (Event.ControlId == "Quad_ButtonQuit") {
			CloseInGameMenu(::EInGameMenuResult::Resume);
			ActionToTrigger = "maniaplanet:quitserver";
		}
		// ---------------------------------- //
		// Return from team selection window
		else if (Event.ControlId == "Quad_TeamsButtonReturn") {
			UpdateWindowsVisibility = True;
			ActiveSubWindow = "";
			LayerExecutableEvents.add("PlaySound:HideDialog");
			(Event.Control as CMlQuad).Opacity = 0.;
		}
		
		// ---------------------------------- //
		// Resume game via help menu
		if (Event.ControlId == "Quad_ButtonHelpResume") {
			CloseInGameMenu(::EInGameMenuResult::Resume);
			(Event.Control as CMlQuad).Opacity = 0.;
		}
		
		// ---------------------------------- //
		// Functional buttons
		if (Frame_PauseButtons.exists(Event.Control.Parent)) {
			(Event.Control as CMlQuad).Opacity = 0.;
			switch (Frame_PauseButtons.keyof(Event.Control.Parent)) {
				// ---------------------------------- //
				// Show mode help
				case 0 : {
					// ---------------------------------- //
					// Display custom manialink window
					if (Net_LibPauseMenu_HelpModeName != "") {
						UpdateWindowsVisibility = True;
						ActiveSubWindow = "ModeRules";
						LayerExecutableEvents.add("PlaySound:ShowDialog");
					}
					// ---------------------------------- //
					// Display legacy window
					else {
						CloseInGameMenu(::EInGameMenuResult::Resume);
						ShowModeHelp();
					}
				}
				// ---------------------------------- //
				// Toggle spectator mode
				case 1 : {
					RequestSpectatorClient(!IsSpectator);
					CloseInGameMenu(::EInGameMenuResult::Resume);
					ActionToTrigger = "maniaplanet:togglespec";
				}
				// ---------------------------------- //
				// Open team selection screen
				case 2 : {
					UpdateWindowsVisibility = True;
					ActiveSubWindow = "TeamSelection";
					LayerExecutableEvents.add("PlaySound:ShowDialog");
				}
				// ---------------------------------- //
				// Save current round replay
				case 3 : {
					CloseInGameMenu(::EInGameMenuResult::Resume);
					ActionToTrigger = "maniaplanet:savereplay";
				}
				// ---------------------------------- //
				// Show advanced server settings
				case 4 : CloseInGameMenu(::EInGameMenuResult::ServerSettings);
				
				// ---------------------------------- //
				// Show advanced pause menu options
				case 5 : CloseInGameMenu(::EInGameMenuResult::AdvancedMenu);
			}
		}
		
		// ---------------------------------- //
		// Join team button
		if (Event.ControlId == "Quad_CardBackground") {
			declare RequestedClan = 0;
			foreach (I => Frame in Frame_TeamsCards) if (Frame.Controls.exists(Event.Control)) {
				declare Label_JoinTeamText <=> (Frame.GetFirstChild("Label_JoinTeamText") as CMlLabel);
				Label_JoinTeamText.Opacity = 0.;
				RequestedClan = I + 1;
			}
			(Event.Control as CMlQuad).Opacity = .125;
			
			// ---------------------------------- //
			// Normal team change
			if (Net_LibPauseMenu_CustomTeams <= 0) {
				switch (RequestedClan) {
					case 1 : JoinTeam1();
					case 2 : JoinTeam2();
				}
			}
			// ---------------------------------- //
			// Custom team change
			else {
				declare netwrite Integer Net_LibPauseMenu_LastReqTime for UI;
				declare netwrite Integer Net_LibPauseMenu_ReqTeamNum for UI;
				Net_LibPauseMenu_LastReqTime = Now;
				Net_LibPauseMenu_ReqTeamNum = RequestedClan;
			}
			
			UpdateWindowsVisibility = True;
			ActiveSubWindow = "";
			CloseInGameMenu(::EInGameMenuResult::Resume);
		}
	}
	// ---------------------------------- //
	// On mouse over
	else if (Event.Type == CMlEvent::Type::MouseOver) {
		// ---------------------------------- //
		// Hover button background
		if (Event.Control.HasClass("hoverButton")) {
			AnimMgr.Add(Event.Control, "<quad opacity='.2' />", 125, CAnimManager::EAnimManagerEasing::SineOut);
			
			foreach (I => Frame in Frame_Buttons) if (Frame.Controls.exists(Event.Control)) {
				declare Label_ButtonText <=> (Frame.GetFirstChild("Label_ButtonText") as CMlLabel);
				AnimMgr.Add(Label_ButtonText, "<label textcolor='FFF' />", 125, CAnimManager::EAnimManagerEasing::SineOut);
			}
		}
		
		// ---------------------------------- //
		// Hover the team join button card
		if (Event.ControlId == "Quad_CardBackground") {
			foreach (I => Frame in Frame_TeamsCards) if (Frame.Controls.exists(Event.Control)) {
				declare Quad = (Event.Control as CMlQuad);
				declare Label_JoinTeamText <=> (Frame.GetFirstChild("Label_JoinTeamText") as CMlLabel);
				
				AnimMgr.Add(Quad, "<quad opacity='.25' />", 250, CAnimManager::EAnimManagerEasing::SineOut);
				AnimMgr.Add(Label_JoinTeamText, "<quad opacity='1' />", 250, CAnimManager::EAnimManagerEasing::SineOut);
			}
		}
	}
	// ---------------------------------- //
	// On mouse out
	else if (Event.Type == CMlEvent::Type::MouseOut) {
		// ---------------------------------- //
		// Unfocus button background
		if (Event.Control.HasClass("hoverButton")) {
			AnimMgr.Add(Event.Control, "<quad opacity='0' />", 125, CAnimManager::EAnimManagerEasing::SineIn);
			
			foreach (I => Frame in Frame_Buttons) if (Frame.Controls.exists(Event.Control)) {
				declare Label_ButtonText <=> (Frame.GetFirstChild("Label_ButtonText") as CMlLabel);
				AnimMgr.Add(Label_ButtonText, "<label textcolor='AAA' />", 125, CAnimManager::EAnimManagerEasing::SineIn);
			}
		}
		
		// ---------------------------------- //
		// Unfocus the team join button card
		if (Event.ControlId == "Quad_CardBackground") {
			foreach (I => Frame in Frame_TeamsCards) if (Frame.Controls.exists(Event.Control)) {
				declare Quad = (Event.Control as CMlQuad);
				declare Label_JoinTeamText <=> (Frame.GetFirstChild("Label_JoinTeamText") as CMlLabel);
				
				AnimMgr.Add(Quad, "<quad opacity='.125' />", 250, CAnimManager::EAnimManagerEasing::SineIn);
				AnimMgr.Add(Label_JoinTeamText, "<quad opacity='0' />", 250, CAnimManager::EAnimManagerEasing::SineIn);
			}
		}
	}
	// ---------------------------------- //
	// On menu navigation
	else if (Event.Type == CMlEvent::Type::MenuNavigation) {
		if (Event.MenuNavAction == CMlEvent::EMenuNavAction::Cancel) {
			// ---------------------------------- //
			// Go up one window
			if (ActiveSubWindow != "") {
				UpdateWindowsVisibility = True;
				ActiveSubWindow = "";
				LayerExecutableEvents.add("PlaySound:HideDialog");
				
				// ---------------------------------- //
				// Unfocus cards in team selection window
				foreach (Frame in Frame_TeamsCards) {
					declare Quad_CardBackground = (Frame.GetFirstChild("Quad_CardBackground") as CMlQuad);
					declare Label_JoinTeamText <=> (Frame.GetFirstChild("Label_JoinTeamText") as CMlLabel);
					Quad_CardBackground.Opacity = .125;
					Label_JoinTeamText.Opacity = 0.;
				}
			}
			// ---------------------------------- //
			// Close pause menu
			else CloseInGameMenu(::EInGameMenuResult::Resume);
			
			// ---------------------------------- //
			// Unfocus all buttons
			foreach (I => Frame in Frame_Buttons) {
				declare Label_ButtonText <=> (Frame.GetFirstChild("Label_ButtonText") as CMlLabel);
				Label_ButtonText.TextColor = TL::ToColor("AAA");
			}
		}
	}
}

// ---------------------------------- //
// Display rules when player plays the mode for the first time
if (
	!PageIsVisible && Net_LibPauseMenu_HelpModeName != "" &&
	InputPlayer != Null && InputPlayer.Position != <0., 0., 0.> &&
	!Persistent_LibPauseMenu_ModesWithRulesRead.exists(Net_LibPauseMenu_HelpModeName)
) {
	Persistent_LibPauseMenu_ModesWithRulesRead.add(Net_LibPauseMenu_HelpModeName);
	ShowInGameMenu();
	UpdateWindowsVisibility = True;
	ActiveSubWindow = "ModeRules";
}

// ---------------------------------- //
// Update pause menu
if (PageIsVisible) {
	// ---------------------------------- //
	// Clans usage change
	if (PrevUseClans != UseClans) {
		PrevUseClans = UseClans;
		UpdatePauseButtons = True;
	}
	if (PrevUseForcedClans != UseForcedClans) {
		PrevUseForcedClans = UseForcedClans;
		UpdatePauseButtons = True;
	}
	
	// ---------------------------------- //
	// Update spectator button check icon
	if (PrevIsSpectatorMode != IsSpectator) {
		PrevIsSpectatorMode = IsSpectator;
		UpdatePauseButtons = True;
	}

	// ---------------------------------- //
	// Update on custom teams amount change
	if (PrevNbCustomTeams != Net_LibPauseMenu_CustomTeams) {
		PrevNbCustomTeams = Net_LibPauseMenu_CustomTeams;
		UpdatePauseButtons = True;
		UpdateTeamsCards = True;
	}

	// ---------------------------------- //
	// Solo mode update
	if (PrevIsSoloMode != Net_LibPauseMenu_IsSoloMode) {
		PrevIsSoloMode = Net_LibPauseMenu_IsSoloMode;
		UpdatePauseButtons = True;
	}
	
	// ---------------------------------- //
	// Set up pause menu buttons frames
	if (UpdatePauseButtons) {
		UpdatePauseButtons = False;
		declare TotalHeight = 0.;
		
		foreach (I => Frame in Frame_PauseButtons) {
			// ---------------------------------- //
			// Set the button text
			declare Label_ButtonText <=> (Frame.GetFirstChild("Label_ButtonText") as CMlLabel);
			Label_ButtonText.SetText(C_LabelsValues[I]);
			
			// ---------------------------------- //
			// Set button visibility
			switch (I) {
				// Toggle spectator mode
				case 1 : Frame.Visible =
					!Net_LibPauseMenu_IsSoloMode &&
					UI.SpectatorObserverMode != CUIConfig::EObserverMode::Forced &&
					UI.SpectatorObserverMode != CUIConfig::EObserverMode::Forbidden;
				
				// Change team
				case 2 : Frame.Visible = !IsSpectator && !UseForcedClans && (UseClans || Net_LibPauseMenu_CustomTeams > 0);

				// Server management
				case 4 : Frame.Visible = !Net_LibPauseMenu_IsSoloMode;

				// Advanced options
				case 5 : Frame.Visible = !Net_LibPauseMenu_IsSoloMode;
				
				// Other
				default : Frame.Visible = True;
			}
			
			// ---------------------------------- //
			// Set the position of visible buttons
			if (Frame.Visible) {
				Frame.RelativePosition_V3.Y = TotalHeight;
				TotalHeight -= 7.;
			}
			
			// ---------------------------------- //
			// Set the spectator mode check icon
			if (Frame.Visible && I == 1) {
				declare Quad_ButtonCheck <=> (Frame.GetFirstChild("Quad_ButtonCheck") as CMlQuad);
				if (IsSpectator) Quad_ButtonCheck.ImageUrl = "{{{C_ImgBase}}}MenuIconCheck1.png";
				else Quad_ButtonCheck.ImageUrl = "{{{C_ImgBase}}}MenuIconCheck0.png";
			}
		}
	}
	
	// ---------------------------------- //
	// Set interface color
	if (GUIPlayer != Null) {
		if (PrevGUIPlayerHue != GUIPlayer.GetLinearHue) {
			PrevGUIPlayerHue = GUIPlayer.GetLinearHue;
			G_InterfaceColor = HsvToRgb(<ML::Clamp(PrevGUIPlayerHue, 0., 1.), 1., 1.>);
		}
	}
	else {
		if (LocalUser != Null) G_InterfaceColor = LocalUser.Color;
		else G_InterfaceColor = C_DefaultInterfaceColor;
		PrevGUIPlayerHue = -1.;
	}
	
	// ---------------------------------- //
	// Update interface color
	if (PrevInterfaceColor != G_InterfaceColor) {
		PrevInterfaceColor = G_InterfaceColor;
		ColorizeControls(ColorizableControls, G_InterfaceColor);
	}
	
	// ---------------------------------- //
	// Team selection window
	if (ActiveSubWindow == "TeamSelection") {
		for (I, 0, 1) {
			// ---------------------------------- //
			// Update on color change
			if (PrevTeamsColors[I] != Teams[I].ColorPrimary) {
				PrevTeamsColors[I] = Teams[I].ColorPrimary;
				UpdateTeamsCards = True;
			}
			
			// ---------------------------------- //
			// Update on name change
			if (PrevTeamsNames[I] != Teams[I].Name) {
				PrevTeamsNames[I] = Teams[I].Name;
				UpdateTeamsCards = True;
			}
			
			// ---------------------------------- //
			// Update on custom team name change
			foreach (Team => Name in Net_LibPauseMenu_TeamsNames) {
				if (PrevCustomTeamsNames.existskey(Team) && PrevCustomTeamsNames[Team] == Name) continue;
				PrevCustomTeamsNames[Team] = Name;
				UpdateTeamsCards = True;
			}
			
			// ---------------------------------- //
			// Update on custom team color change
			foreach (Team => Color in Net_LibPauseMenu_TeamsColors) {
				if (PrevCustomTeamsColors.existskey(Team) && PrevCustomTeamsColors[Team] == Color) continue;
				PrevCustomTeamsColors[Team] = Color;
				UpdateTeamsCards = True;
			}
			
			// ---------------------------------- //
			// Update players amount
			declare ClansNbPlayers = [0 => 0, 1 => 0, 2 => 0];
			foreach (Player in Players)
				if (!Player.RequestsSpectate && !Player.IsBot) ClansNbPlayers[Player.CurrentClan] += 1;
			
			for (I, 0, 2) if (PrevClansNbPlayers[I] != ClansNbPlayers[I]) {
				PrevClansNbPlayers[I] = ClansNbPlayers[I];
				UpdateTeamsCards = True;
			}
			
			// ---------------------------------- //
			// Update cards
			if (UpdateTeamsCards) {
				UpdateTeamsCards = False;
				
				// Get number of teams
				declare NbTeams = Teams.count;
				if (Net_LibPauseMenu_CustomTeams > 0) NbTeams = Net_LibPauseMenu_CustomTeams;
				
				// Get teams colors
				declare TeamsColors = [1 => Teams[0].ColorPrimary, 2 => Teams[1].ColorPrimary];
				foreach (Team => Color in Net_LibPauseMenu_TeamsColors) TeamsColors[Team] = Color;
				
				// Get teams names
				declare TeamsNames = [1 => Teams[0].Name, 2 => Teams[1].Name];
				foreach (Team => Name in Net_LibPauseMenu_TeamsNames) TeamsNames[Team] = Name;
				
				// ---------------------------------- //
				// Format the teams window
				Quad_TeamsWindowBg.Size.X = NbTeams * 50.;
				Quad_TeamsWindowBlur.Size.X = Quad_TeamsWindowBg.Size.X;
				Quad_TeamsWindowShad.Size.X = Quad_TeamsWindowBg.Size.X * 2. + 10.;
				Quad_TeamsHeaderBg.Size.X = Quad_TeamsWindowBg.Size.X - 1;
				Frame_TeamsCardsCon.RelativePosition_V3.X = 25. + Quad_TeamsWindowBg.Size.X / -2.;
				
				foreach (I => Frame in Frame_TeamsCards) {
					declare Clan = I + 1;
					Frame.Visible = Clan <= NbTeams;
					if (!Frame.Visible) continue;
					
					// Frame position
					Frame.RelativePosition_V3.X = I * 50.;
					
					// ---------------------------------- //
					// Set card background color
					declare Quad_CardBackground <=> (Frame.GetFirstChild("Quad_CardBackground") as CMlQuad);
					if (TeamsColors.existskey(Clan)) Quad_CardBackground.BgColor = TeamsColors[Clan];
					else Quad_CardBackground.BgColor = <1., 1., 1.>;
					
					// ---------------------------------- //
					// Set team emblem
					declare Quad_TeamEmblem <=> (Frame.GetFirstChild("Quad_TeamEmblem") as CMlQuad);
					declare Emblem = Clan;
					if (Net_LibPauseMenu_CustomTeams > 0) Emblem = 0;
					Quad_TeamEmblem.Substyle = "#"^Emblem;
					
					// ---------------------------------- //
					// Set team name
					declare Label_TeamName <=> (Frame.GetFirstChild("Label_TeamName") as CMlLabel);
					declare TeamName = "???";
					if (TeamsNames.existskey(Clan)) TeamName = TeamsNames[Clan];
					Label_TeamName.SetText("$"^TL::ColorToText(Quad_CardBackground.BgColor)^TeamName);
					
					// ---------------------------------- //
					// Set players amount
					declare Label_TeamNbPlayers <=> (Frame.GetFirstChild("Label_TeamNbPlayers") as CMlLabel);
					Label_TeamNbPlayers.Visible = Net_LibPauseMenu_CustomTeams <= 0;
					if (Label_TeamNbPlayers.Visible)
						Label_TeamNbPlayers.SetText(TL::Compose("$t%1: %2", _("Players"), TL::ToText(ClansNbPlayers[Clan])));
				}
			}
		}
	}
	
	// ---------------------------------- //
	// Mode rules window
	if (ActiveSubWindow == "ModeRules") {
		// ---------------------------------- //
		// Update help on mode name change
		if (PrevModeName != Net_LibPauseMenu_HelpModeName) {
			PrevModeName = Net_LibPauseMenu_HelpModeName;
			UpdateHelpWindow = 2;
		}
		
		// ---------------------------------- //
		// Update help window
		if (UpdateHelpWindow > 0) {
			UpdateHelpWindow -= 1;
			declare TotalHeight = 12.;
			
			// ---------------------------------- //
			// Set the mode name
			Label_HelpModeName.SetText(Net_LibPauseMenu_HelpModeName);
			
			// ---------------------------------- //
			// Set the mode objectives
			Label_HelpModeObjectives.SetText(Translate(Net_LibPauseMenu_HelpModeObjectives));
			Label_HelpModeObjectives.RelativePosition_V3.Y = -TotalHeight;
			TotalHeight += Label_HelpModeObjectives.ValueLineCount * 4.;
			
			// ---------------------------------- //
			// Set the resume button position
			TotalHeight += 1.;
			Frame_ButtonHelpResume.RelativePosition_V3.Y = -TotalHeight;
			TotalHeight += 8.;
			
			// ---------------------------------- //
			// Set the frame final size
			TotalHeight += 1.;
			Quad_HelpBgColor.Size.Y = TotalHeight;
			Quad_HelpBgBlur.Size.Y = TotalHeight;
			Quad_HelpBgShadow.Size.Y = TotalHeight * 2 + 10;
			Frame_HelpWindow.RelativePosition_V3.Y = TotalHeight / 2;
		}
	}
}

// ---------------------------------- //
// Play sound on pause visibility change
if (PrevPageIsVisible != PageIsVisible) {
	PrevPageIsVisible = PageIsVisible;
	
	if (PageIsVisible)
		LayerExecutableEvents.add("PlaySound:ShowDialog");
	else {
		LayerExecutableEvents.add("PlaySound:HideDialog");
		UpdateWindowsVisibility = True;
		ActiveSubWindow = "";

		foreach (Control in ColorizableControls) {
			if (Control is CMlQuad) {
				declare Quad = (Control as CMlQuad);
				Quad.Opacity = 0.;
			}
		}
	}
}

// ---------------------------------- //
// Update windows visibility
if (UpdateWindowsVisibility) {
	UpdateWindowsVisibility = False;
	Frame_PauseWindow.Visible = ActiveSubWindow == "";
	Frame_TeamSelection.Visible = ActiveSubWindow == "TeamSelection";
	Frame_HelpWindow.Visible = ActiveSubWindow == "ModeRules";
}
***

// ---------------------------------- //
// Functions
// ---------------------------------- //

// ---------------------------------- //
/** Colorize array of manialink controls
 *
 *	@param	_Controls	Elements to colorize
 *	@param	_Color		Color of the elements
 */
Void ColorizeControls(CMlControl[] _Controls, Vec3 _Color) {
	if (_Controls.count <= 0) return;
	foreach (Control in _Controls) {
		switchtype (Control) {
			case CMlQuad : {
				declare Quad = (Control as CMlQuad);
				Quad.BgColor = _Color;
				Quad.Colorize = _Color;
			}
		}
	}
}

{{{ManialinkTools::Function("HsvToRgb")}}}
{{{Translations::InsertToManialink([Net_LibPauseMenu_HelpModeObjectives])}}}

// ---------------------------------- //
// Main
// ---------------------------------- //
main() {
	+++Main+++
	while (True) {
		yield;
		+++Yield+++
	}
}
--></script>
</manialink>""";
}

// ---------------------------------- //
/** Create manialink for the executable layer
 *
 *	@return		The executable layer
 */
Text Private_CreateManialinkExecutable() {
	return """
<manialink version="2" name="LibPauseMenu:Executable">
<script><!--
/**
 *	LibPauseMenu: Executable
 */

#Include "TextLib" as TL

// ---------------------------------- //
// Main
// ---------------------------------- //
***Main***
***
// ---------------------------------- //
// Variables
declare Text[] LayerExecutableEvents for This;
***

// ---------------------------------- //
// Yield
// ---------------------------------- //
***Yield***
***
// ---------------------------------- //
// Manage custom events
while (LayerExecutableEvents.existskey(0)) {
	declare EventData = TL::Split(":", LayerExecutableEvents[0]);
	
	// ---------------------------------- //
	// Switch event type
	switch (EventData[0]) {
		// ---------------------------------- //
		// Play interface sound
		case "PlaySound" : {
			declare CAudioManager::ELibSound Sound;
			if (EventData.existskey(1)) switch (EventData[1]) {
				case "ShowDialog" : Sound = CAudioManager::ELibSound::ShowDialog;
				case "HideDialog" : Sound = CAudioManager::ELibSound::HideDialog;
			}
			Audio.PlaySoundEvent(Sound, 0, 20.);
		}
	}
	
	// Destroy event
	declare Removed = LayerExecutableEvents.removekey(0);
}
***

// ---------------------------------- //
// Functions
// ---------------------------------- //

// ---------------------------------- //
// Main
// ---------------------------------- //
main() {
	+++Main+++
	while (True) {
		+++Yield+++
		yield;
	}
}
--></script>
</manialink>""";
}

// ---------------------------------- //
// Public
// ---------------------------------- //

// ---------------------------------- //
/** Return the version number of the script
 *
 *	@return		The version number of the script
 */
Text GetScriptVersion() { return Version; }

// ---------------------------------- //
/** Return the name of the script
 *
 *	@return		The name of the script
 */
Text GetScriptName() { return ScriptName; }

// ---------------------------------- //
/** Set the rules of the game mode
 *
 *	@param	_ModeName			Name of the game mode
 *	@param	_ModeObjectives		Objectives of the game mode
 *	@param	_ModeImage			Image displayed over objectives
 */
Void SetModeRules(Text _ModeName, Text _ModeObjectives, Text _ModeImage) {
	if (_ModeName == "" || _ModeObjectives == "") return;
	declare netwrite Text Net_LibPauseMenu_HelpModeName for Teams[0];
	declare netwrite Text Net_LibPauseMenu_HelpModeObjectives for Teams[0];
	declare netwrite Text Net_LibPauseMenu_HelpModeImage for Teams[0];
	
	Net_LibPauseMenu_HelpModeName = _ModeName;
	Net_LibPauseMenu_HelpModeObjectives = _ModeObjectives;
	if (Http.IsValidUrl(_ModeImage)) Net_LibPauseMenu_HelpModeImage = _ModeImage;
}

// ---------------------------------- //
/** Set amount of custom teams
 *
 *	@param	_Count	Custom teams amount
 */
Void SetCustomTeamsCount(Integer _Count) {
	declare netwrite Integer Net_LibPauseMenu_CustomTeams for Teams[0];
	Net_LibPauseMenu_CustomTeams = ML::Max(_Count, 0);
}

// ---------------------------------- //
/** Set color of a custom team
 *
 *	@param	_Team	The team to tweak
 *	@param	_Color	Color of the team
 */
Void SetCustomTeamColor(Integer _Team, Vec3 _Color) {
	declare netwrite Vec3[Integer] Net_LibPauseMenu_TeamsColors for Teams[0];
	Net_LibPauseMenu_TeamsColors[_Team] = _Color;
}

// ---------------------------------- //
/** Set name of a custom team
 *
 *	@param	_Team	The team to tweak
 *	@param	_Name	Name of the team
 */
Void SetCustomTeamName(Integer _Team, Text _Name) {
	declare netwrite Text[Integer] Net_LibPauseMenu_TeamsNames for Teams[0];
	Net_LibPauseMenu_TeamsNames[_Team] = _Name;
}

Void SetSoloMode(Boolean _IsSolo) {
	declare netwrite Boolean Net_LibPauseMenu_IsSoloMode for Teams[0];
	Net_LibPauseMenu_IsSoloMode = _IsSolo;
}

// ---------------------------------- //
/** Get players team change requests
 *
 *	@return		Array of requested teams numbers, keyed by players Ids
 */
Integer[Ident] CustomTeamsRequests() {
	if (G_LibPauseMenu_TeamChangeRequests.count > 0) G_LibPauseMenu_TeamChangeRequests.clear();
	
	// ---------------------------------- //
	// Get the new requests
	foreach (Player in AllPlayers) {
		if (Player.IsBot || Player.IsFakePlayer) continue;
		declare UI <=> UIManager.GetUI(Player);
		if (UI == Null) continue;
		
		declare Integer LastReqTime for Player;
		declare netread Integer Net_LibPauseMenu_LastReqTime for UI;
		
		if (LastReqTime == Net_LibPauseMenu_LastReqTime) continue;
		LastReqTime = Net_LibPauseMenu_LastReqTime;
		
		declare netread Integer Net_LibPauseMenu_ReqTeamNum for UI;
		G_LibPauseMenu_TeamChangeRequests[Player.Id] = Net_LibPauseMenu_ReqTeamNum;
	}
	
	return G_LibPauseMenu_TeamChangeRequests;
}

// ---------------------------------- //
/// Unload the library
Void Unload() {
	Layers::Detach("LibPauseMenu:Pause");
	Layers::Detach("LibPauseMenu:Executable");
	Layers::Destroy("LibPauseMenu:Pause");
	Layers::Destroy("LibPauseMenu:Executable");
	
	declare netwrite Text Net_LibPauseMenu_HelpModeName for Teams[0];
	declare netwrite Text Net_LibPauseMenu_HelpModeObjectives for Teams[0];
	declare netwrite Text Net_LibPauseMenu_HelpModeImage for Teams[0];
	declare netwrite Integer Net_LibPauseMenu_CustomTeams for Teams[0];
	declare netwrite Vec3[Integer] Net_LibPauseMenu_TeamsColors for Teams[0];
	declare netwrite Text[Integer] Net_LibPauseMenu_TeamsNames for Teams[0];
	
	Net_LibPauseMenu_HelpModeName = "";
	Net_LibPauseMenu_HelpModeObjectives = "";
	Net_LibPauseMenu_HelpModeImage = "";
	Net_LibPauseMenu_CustomTeams = -1;
	Net_LibPauseMenu_TeamsColors.clear();
	Net_LibPauseMenu_TeamsNames.clear();
}

// ---------------------------------- //
/// Load the library
Void Load() {
	Unload();
	
	// Create the main interface layer
	Layers::Create("LibPauseMenu:Pause", Private_CreateManialinkPauseMenu());
	Layers::SetType("LibPauseMenu:Pause", CUILayer::EUILayerType::InGameMenu);
	Layers::Attach("LibPauseMenu:Pause");
	
	// Create the executable interface layer
	Layers::Create("LibPauseMenu:Executable", Private_CreateManialinkExecutable());
	Layers::Attach("LibPauseMenu:Executable");
}