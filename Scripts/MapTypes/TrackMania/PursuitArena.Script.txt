// -------------------------------------- //
//  PURSUIT TM ARENA by domino54          //
//  script version: 2018-04-08            //
// -------------------------------------- //

#RequireContext CTmMapType

#Const Version			"2018-04-08"
#Const MapTypeVersion	1
#Const ScriptName		"MapTypes/TrackMania/PursuitArena.Script.txt"

#Include "MathLib" as ML
#Include "TextLib" as TL
#Include "Libs/Nadeo/Anchor.Script.txt" as Anchor
#Include "Libs/Nadeo/MapType.Script.txt" as MapType
#Include "Libs/domino54/EditAnchors.Script.txt" as EditAnchors
#Include "Libs/domino54/TrackMania/OffZoneEdit.Script.txt" as OffZone

#Const C_Rules "$<%11.$> Place exactly one StartFinish.\n$<%12.$> Place at least one Checkpoint.\n$<%13.$>Place exactly one Podium."

// ---------------------------------- //
// Functions
// ---------------------------------- //

// ---------------------------------- //
/// Initialize the anchors
Void InitAnchors() {
	foreach (Anchor in AnchorData) {
		Anchor.Order = Anchor.DefaultOrder;
		Anchor.Tag = Anchor.DefaultTag;
	}
}

// ---------------------------------- //
/// Check if the map is valid
Void UpdateValidability() {
	ValidationStatus = CSmMapType::ValidationStatus::NotValidable;
	InitAnchors();
	Anchor::UpdateAnchorCounts();
	
	// ---------------------------------- //
	// Check if the map has spawn and checkpoints
	if (!Anchor::HasExactlyOneAnchor("StartFinish", 0, _("You must place exactly one StartFinish."))) return;
	if (!Anchor::HasAtLeastOneAnchor("Checkpoint", 0, _("You must place at least one Checkpoint."))) return;
	
	declare HasPodium = False;
	foreach (Block in Blocks) {
		if (Block.BlockModel == Null || TL::Find("Podium", Block.BlockModel.Name, False, False)) continue;
		HasPodium = True;
	}
	if (!HasPodium) return;
	
	// Map is valid
	ValidationStatus = CSmMapType::ValidationStatus::Validated;	
}

// ---------------------------------- //
/// Show the anchor edition manialink
Void EditAnchorData(Ident _EditedAnchorDataId) {
	if (_EditedAnchorDataId == NullId) return;
	UpdateValidability();
	EditAnchors::EditAnchor(_EditedAnchorDataId);
	UpdateValidability();
}

// ---------------------------------- //
// Main
// ---------------------------------- //
main() {
	MapType::SetVersion(MapTypeVersion);
	OffZone::Load();

	UpdateValidability();
	CustomEditAnchorData = True;
	
	EditAnchors::SetRulesText(TL::Compose(C_Rules, "$o$070"));
	EditAnchors::SetAvailableTags(["Checkpoint" => ["Checkpoint", "Goal"]]);

	// ---------------------------------- //
	// Yield
	// ---------------------------------- //
	while (True) {
		yield;

		OffZone::Loop();
		
		// "Validate" the map
		Map.TMObjective_AuthorTime = 1;
		Map.TMObjective_GoldTime = 1;
		Map.TMObjective_SilverTime = 1;
		Map.TMObjective_BronzeTime = 1;
		Map.TMObjective_NbLaps = 1;
		Map.TMObjective_IsLapRace = False;
		Map.ObjectiveTextAuthor = "N/A";
		Map.ObjectiveTextGold = "N/A";
		Map.ObjectiveTextSilver = "N/A";
		Map.ObjectiveTextBronze = "N/A";

		// ---------------------------------- //
		// Events management
		foreach (Event in PendingEvents) {
			switch (Event.Type) {
				case CPluginEvent::Type::EditAnchor : EditAnchorData(Event.EditedAnchorDataId);
				case CPluginEvent::Type::StartValidation : { }
			}
			UpdateValidability();
		}
	}
}